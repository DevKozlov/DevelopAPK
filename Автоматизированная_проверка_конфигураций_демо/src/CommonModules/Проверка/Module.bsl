
#Область ПроведениеДокументаПроверкаВерсии

Процедура ОбработкаПроведенияДокументаПроверки(ПроверкаВерсии) Экспорт
	
	Конфигурация = ПроверкаВерсии.Конфигурация;
	Версия = ПроверкаВерсии.Версия;
	Дата = ПроверкаВерсии.Дата;
	ЖурналПроверки = ПроверкаВерсии.ЖурналПроверки;
	РегистрироватьВсеОшибкиКакОсобенности = ПроверкаВерсии.РегистрироватьВсеОшибкиКакОсобенности;
	
	ПараметрыПроведения = Новый Структура;
	
	ИспользоватьРегистрациюВСППР = ИнтеграцияССППР.ИспользоватьРегистрациюВСППР(Конфигурация);
	
	СтруктураПроверкиДляРегистрацииВСППР = Новый Структура;
	СтруктураПроверкиДляРегистрацииВСППР.Вставить("Конфигурация", Конфигурация);
	СтруктураПроверкиДляРегистрацииВСППР.Вставить("Версия", Версия);
	СтруктураПроверкиДляРегистрацииВСППР.Вставить("ЖурналПроверки", ЖурналПроверки);
	СтруктураПроверкиДляРегистрацииВСППР.Вставить("Дата", Дата);
	СтруктураПроверкиДляРегистрацииВСППР.Вставить("ИспользоватьРегистрациюВСППР", ИспользоватьРегистрациюВСППР);
	СтруктураПроверкиДляРегистрацииВСППР.Вставить("ГруппироватьОшибкиПоОтветственному",
		Конфигурация.ГруппироватьОшибкиПоОтветственному);
	СтруктураПроверкиДляРегистрацииВСППР.Вставить("ВерсияКонфигурацииСППР", Конфигурация.ВерсияКонфигурацииСППР);
	СтруктураПроверкиДляРегистрацииВСППР.Вставить("ДополнениеКТекстуОшибкиСППР",
		Конфигурация.ДополнениеКТекстуОшибкиСППР);
	СтруктураПроверкиДляРегистрацииВСППР.Вставить("ИмяКонфигурацииСППР", Конфигурация.ИмяКонфигурацииСППР);
	СтруктураПроверкиДляРегистрацииВСППР.Вставить("ПолучатьОтветственныхЗаМетаданныеИзСППР",
		Конфигурация.ПолучатьОтветственныхЗаМетаданныеИзСППР);
	
	Если ИспользоватьРегистрациюВСППР Тогда
		Если НЕ ИнтеграцияССППР.ПроверитьСоединениеССППР(СтруктураПроверкиДляРегистрацииВСППР) Тогда
			ЖурналПроверки = СтруктураПроверкиДляРегистрацииВСППР.ЖурналПроверки;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Устанавливаем максимальное количество ошибок, предполагаем, что больше 1 млрд ошибок не будет.
	МаксимальноеКоличествоОшибокОдногоВида = ?(РегистрироватьВсеОшибкиКакОсобенности, 1000000000, 10000);
	ПараметрыПроведения.Вставить("МаксимальноеКоличествоОшибокОдногоВида", МаксимальноеКоличествоОшибокОдногоВида);
	
	Сценарий = Конфигурация.Сценарий;
	АнализКонфигурации = ((Сценарий = 0) ИЛИ (Сценарий = 2));
	АнализРасширений = (Сценарий > 0);
	
	Если АнализКонфигурации Тогда
		ПровестиДокумент(ПроверкаВерсии, ПараметрыПроведения, ЖурналПроверки, СтруктураПроверкиДляРегистрацииВСППР);
	КонецЕсли;
	
	Если АнализРасширений Тогда
		Для Каждого СтрокаРасширения Из Конфигурация.Расширения Цикл
			ПровестиДокумент(ПроверкаВерсии, ПараметрыПроведения, ЖурналПроверки, СтруктураПроверкиДляРегистрацииВСППР,
				СтрокаРасширения.Расширение);
		КонецЦикла;
	КонецЕсли;
	
	ПроверкаВерсии.ЖурналПроверки = ЖурналПроверки;
	
КонецПроцедуры

Процедура ПровестиДокумент(ПроверкаВерсии, ПараметрыПроведения, ЖурналПроверки, СтруктураПроверкиДляРегистрацииВСППР,
	Знач Расширение = Неопределено)
	
	РегистрироватьВсеОшибкиКакОсобенности = ПроверкаВерсии.РегистрироватьВсеОшибкиКакОсобенности;
	ОбнаруженоОшибок = ПроверкаВерсии.ОбнаруженоОшибок;
	
	Расширение = ПолучитьСсылкуРасширения(Расширение);
	
	ЗаполнитьКэшируемыеЗначения(ПроверкаВерсии, Расширение, ПараметрыПроведения);
	
	ПроверитьОбъекты(Расширение, ПроверкаВерсии, ПараметрыПроведения, ЖурналПроверки);
	
	// Удалим из ошибок помеченные на удаление объекты.
	УдалитьОшибкиОбъектовПомеченныхНаУдаление(Расширение, ПроверкаВерсии, ЖурналПроверки);
	
	// Удалим из ошибок исключенные объекты конфигурации.
	УдалитьОшибкиИсключенныхОбъектов(Расширение, ПроверкаВерсии, ЖурналПроверки);
	
	СинхронизироватьОшибкиИОсобенности(Расширение, ПроверкаВерсии, ПараметрыПроведения, ЖурналПроверки);
	
	// Если необходимо зарегистрировать все ошибки как особенности, то нет ограничения по количеству ошибок.
	Если РегистрироватьВсеОшибкиКакОсобенности Тогда
		// Можно вынести из цикла
		ЗарегистрироватьВсеОшибкиКакОсобенности(Расширение, ПроверкаВерсии, ПараметрыПроведения, ЖурналПроверки);
	Иначе
		// Иначе дополнительно регистрируем ошибки за конфигурацию с ограничением в 10000 ошибок.
		ДобавитьОшибкиЗаКонфигурацию(Расширение, ПроверкаВерсии, ПараметрыПроведения);
	КонецЕсли;
	
	Если СтруктураПроверкиДляРегистрацииВСППР.ИспользоватьРегистрациюВСППР Тогда
		
		СтруктураПроверкиДляРегистрацииВСППР.Вставить("ЖурналПроверки",  ЖурналПроверки);
		СтруктураПроверкиДляРегистрацииВСППР.Вставить("ПравилаПроверки", ПараметрыПроведения.ПравилаПроверки);
		
		ТаблицаОшибокАПК = ПараметрыПроведения.ТаблицаОшибокАПК;
		
		ИнтеграцияССППР.ЗарегистрироватьОшибкиСППР(СтруктураПроверкиДляРегистрацииВСППР, ТаблицаОшибокАПК);
		
		ЖурналПроверки = СтруктураПроверкиДляРегистрацииВСППР.ЖурналПроверки;
		Если СтруктураПроверкиДляРегистрацииВСППР.Свойство("ОбнаруженоОшибок") Тогда
			ОбнаруженоОшибок = СтруктураПроверкиДляРегистрацииВСППР.ОбнаруженоОшибок;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаписатьОшибкиАПК(Расширение, ПроверкаВерсии, ПараметрыПроведения, ЖурналПроверки);
	ПеренестиКомментарии(Расширение, ПроверкаВерсии, ПараметрыПроведения, ЖурналПроверки);
	
	ПроверкаВерсии.ОбнаруженоОшибок = ОбнаруженоОшибок;
	
КонецПроцедуры

Процедура ЗаполнитьКэшируемыеЗначения(ПроверкаВерсии, Расширение, ПараметрыПроведения)
	
	// Выполним кэширование вспомогательной информации.
	Требования = ПроверкаВерсии.СоставТребований.ВыгрузитьКолонку("Требование");
	
	ЗапросПоПравилам = Новый Запрос;
	ЗапросПоПравилам.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТребованияРеализацияТребования.ПравилоПроверки КАК Правило
	|ИЗ
	|	Справочник.Требования.РеализацияТребования КАК ТребованияРеализацияТребования
	|ГДЕ
	|	ТребованияРеализацияТребования.Ссылка В(&Требования)";
	
	ЗапросПоПравилам.УстановитьПараметр("Требования", Требования);
	
	ПравилаПроверки = ЗапросПоПравилам.Выполнить().Выгрузить().ВыгрузитьКолонку("Правило");
	
	ЗапросПоОшибкам = Новый Запрос;
	ЗапросПоОшибкам.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПравилаОбнаруживаемыеОшибки.Ошибка КАК Ошибка
	|ИЗ
	|	Справочник.Правила.ОбнаруживаемыеОшибки КАК ПравилаОбнаруживаемыеОшибки
	|ГДЕ
	|	ПравилаОбнаруживаемыеОшибки.Ссылка В(&ПравилаПроверки)";
	
	ПравилаПлатформеннойПроверки = Новый Массив;
	ОшибкиПлатформеннойПроверки = Новый Массив;
	
	// Если данные о платформенной проверке не собраны, то запомним правила и ошибки платформенной проверки.
	Если НЕ ВерсияПолучитьФлагСбораДанных(ПроверкаВерсии.Версия, "СобраныДанныеПоПроверкеКонфигурации", Расширение) Тогда
		ПравилаПлатформеннойПроверки = ПолучитьПравилаПлатформеннойПроверки();
		
		ЗапросПоОшибкам.УстановитьПараметр("ПравилаПроверки", ПравилаПлатформеннойПроверки);
		ОшибкиПлатформеннойПроверки = ЗапросПоОшибкам.Выполнить().Выгрузить().ВыгрузитьКолонку("Ошибка");
	КонецЕсли;
	
	ПараметрыПроведения.Вставить("ПравилаПлатформеннойПроверки", ПравилаПлатформеннойПроверки);
	ПараметрыПроведения.Вставить("ОшибкиПлатформеннойПроверки", ОшибкиПлатформеннойПроверки);
	ПараметрыПроведения.Вставить("ПравилаПроверки", ПравилаПроверки);
	ПараметрыПроведения.Вставить("Требования", Требования);
	
КонецПроцедуры

Функция ПолучитьПравилаПлатформеннойПроверки()
	
	ТребованиеПлатформеннойПроверки = Справочники.Требования.ПлатформеннаяПроверкаКонфигурации;
	
	Если НЕ ЗначениеЗаполнено(ТребованиеПлатформеннойПроверки) Тогда
		НаименованиеТребования = НСтр("ru='Платформенная проверка конфигурации'");
		ТребованиеПлатформеннойПроверки = Справочники.Требования.НайтиПоНаименованию(НаименованиеТребования);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТребованиеПлатформеннойПроверки) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	ПравилаПлатформеннойПроверки = ТребованиеПлатформеннойПроверки.РеализацияТребования.ВыгрузитьКолонку("ПравилоПроверки");
	
	Возврат ПравилаПлатформеннойПроверки;
	
КонецФункции

Процедура ПроверитьОбъекты(Расширение, ПроверкаВерсии, ПараметрыПроведения, ЖурналПроверки)
	
	Версия = ПроверкаВерсии.Версия;
	Конфигурация = ПроверкаВерсии.Конфигурация;
	СоставОбъектов = ПроверкаВерсии.СоставОбъектов;
	
	ПравилаПроверкиПоТипу = ПолучитьСоответствиеПравилПроверкиПоТипу(ПараметрыПроведения);
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	// Инициализируем ядро для проверки.
	Ядро = Обработки.Ядро.Создать();
	Ядро.УстановитьКонтекстРасширения(Расширение);
	Ядро.УстановитьКонтекстВерсии(Версия);
	Ядро.УстановитьРегистрациюВТаблицу();
	Ядро.УстановитьМаксимальноеКоличествоОшибокОдногоВида(ПараметрыПроведения.МаксимальноеКоличествоОшибокОдногоВида);
	
	Текст = НСтр("ru='Начало сбора исключений из проверки объектов %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Информация, Текст, ЖурналПроверки);
	
	Ядро.ЗаполнитьТаблицуИсключенийИзПроверки();
	
	Текст = НСтр("ru='Сбор исключений из проверки объектов %1 завершен'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Информация, Текст, ЖурналПроверки);
	
	ПроверенныеОбъекты = Новый Массив;
	
	// Определяем количество объектов для проверки.
	ЗапросПоОбъектам = Новый Запрос;
	ЗапросПоОбъектам.Текст = "
	|ВЫБРАТЬ
	|	СтруктураКонфигурации.Ссылка КАК Ссылка,
	|	СтруктураКонфигурации.Код КАК Код,
	|	СтруктураКонфигурации.Наименование КАК Наименование,
	|	СтруктураКонфигурации.ТипОбъекта КАК ТипОбъекта,
	|	СтруктураКонфигурации.Путь КАК Путь,
	|	СтруктураКонфигурации.Родитель КАК Родитель,
	|	СтруктураКонфигурации.Родитель.ТипОбъекта КАК РодительТипОбъекта,
	|	СтруктураКонфигурации.СобраныСведения КАК СобраныСведения
	|ИЗ
	|	Справочник.СтруктураКонфигурации КАК СтруктураКонфигурации
	|ГДЕ
	|	СтруктураКонфигурации.Владелец = &Владелец
	|	И СтруктураКонфигурации.СобраныСведения
	|	И НЕ СтруктураКонфигурации.ПометкаУдаления
	|	И НЕ СтруктураКонфигурации.ТипОбъекта = &ТипОбъектаКорень
	|	И СтруктураКонфигурации.Расширение = &Расширение
	|УПОРЯДОЧИТЬ ПО
	|	НомерПоПорядку";
	
	ЗапросПоОбъектам.УстановитьПараметр("Расширение", Расширение);
	ЗапросПоОбъектам.УстановитьПараметр("Владелец", Версия);
	ЗапросПоОбъектам.УстановитьПараметр("ТипОбъектаКорень", Перечисления.ТипыОбъектов.Корень);
	
	ВыборкаОбъектов = ЗапросПоОбъектам.Выполнить().Выбрать();
	ВсегоОбъектов = ВыборкаОбъектов.Количество();
	
	Счетчик = 0;
	
	Текст = НСтр("ru='Начало проверки %1 на соответствие стандартам'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Информация, Текст, ЖурналПроверки);
	
	ТекстСостоянияШаблон = НСтр("ru='Всего объектов: %1. Проверяется объект №%2 - %3'");
	
	ЭтоПроверкаРасширения = ЗначениеЗаполнено(Расширение);
	ПропуститьОбъектыСПрефиксомУдалить = НЕ Конфигурация.ПроверятьОбъектыСПрефиксомУдалить;
	
	// Последовательно проверяем каждый объект структуры метаданных конфигурации.
	ОсобыйСостав = (СоставОбъектов.Количество() > 0);
	Пока ВыборкаОбъектов.Следующий() Цикл
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
		Счетчик = Счетчик + 1;
		
		ОбъектСсылка = ВыборкаОбъектов.Ссылка;
		ОбъектНаименование = ВыборкаОбъектов.Наименование;
		ОбъектТип = ВыборкаОбъектов.ТипОбъекта;
		
		Если ПропуститьОбъектыСПрефиксомУдалить Тогда
			Если СтрНачинаетсяС(ВРег(ОбъектНаименование), ВРег("Удалить")) Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если ОсобыйСостав Тогда
			Если СоставОбъектов.Найти(ОбъектСсылка, "Объект") = Неопределено Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		// Пропускаем проверку ролей с префиксом "Профиль_".
		Если ОбъектТип = Перечисления.ТипыОбъектов.Роль Тогда
			Если СтрНачинаетсяС(ВРег(ОбъектНаименование), ВРег("Профиль_")) Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		#Если Клиент Тогда
		ТекстСостояния = СтрШаблон(ТекстСостоянияШаблон, ВсегоОбъектов, Счетчик, ВыборкаОбъектов.Путь);
		Состояние(ТекстСостояния);
		#КонецЕсли
		
		Ядро.ПроверитьОбъект(Неопределено, ВыборкаОбъектов, ПравилаПроверкиПоТипу);
		
		ПроверенныеОбъекты.Добавить(ОбъектСсылка);
		
	КонецЦикла;
	
	Ядро.ЗавершитьПроверку();
	НайденныеОшибки = Ядро.ПолучитьНайденныеОшибки();
	СоответствиеОшибок = Ядро.ПолучитьСоответствиеОбнаруженныхОшибок();
	
	ПараметрыПроведения.Вставить("НайденныеОшибки", НайденныеОшибки);
	ПараметрыПроведения.Вставить("СоответствиеОшибок", СоответствиеОшибок);
	ПараметрыПроведения.Вставить("ПроверенныеОбъекты", ПроверенныеОбъекты);
	
	Ядро = Неопределено;
	
	// Валидируем проверку требований.
	Для Каждого Требование Из ПараметрыПроведения.Требования Цикл
		ВалидироватьПроверкуТребования(Требование);
	КонецЦикла;
	
	Текст = НСтр("ru='Проверка %1 на соответствие стандартам завершена'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Информация, Текст, ЖурналПроверки);
	
КонецПроцедуры

Функция ПолучитьСоответствиеПравилПроверкиПоТипу(ПараметрыПроведения)
	
	ПравилаПлатформеннойПроверки = ПараметрыПроведения.ПравилаПлатформеннойПроверки;
	
	// Определяем списки правил для проверки объектов по типам.
	ПравилаПроверкиПоТипу = Новый Соответствие;
	
	ЗапросПоПравилам = Новый Запрос;
	ЗапросПоПравилам.Текст = "
	|ВЫБРАТЬ
	|	ПравилаОбслуживаемыеТипы.ТипОбъекта,
	|	ПравилаОбслуживаемыеТипы.Ссылка
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТребованияРеализацияТребования.ПравилоПроверки КАК Правило
	|	ИЗ
	|		Справочник.Требования.РеализацияТребования КАК ТребованияРеализацияТребования
	|	ГДЕ
	|		ТребованияРеализацияТребования.Ссылка В(&СписокТребований)
	|		И ТребованияРеализацияТребования.Ссылка.СпособПроверки <> &ТребованияОтменено) КАК ВложенныйЗапрос
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Правила.ОбслуживаемыеТипы КАК ПравилаОбслуживаемыеТипы
	|		ПО ВложенныйЗапрос.Правило = ПравилаОбслуживаемыеТипы.Ссылка
	|ГДЕ
	|	ВложенныйЗапрос.Правило.ИспользуетсяПриПроверке
	|	И НЕ ПравилаОбслуживаемыеТипы.Ссылка.РучнаяПроверка
	|	%1";
	
	ЗапросПоПравилам.УстановитьПараметр("СписокТребований", ПараметрыПроведения.Требования);
	ЗапросПоПравилам.УстановитьПараметр("ТребованияОтменено", Перечисления.СпособПроверки.Отменено);
	
	УсловиеПоПлатформеннойПроверке = "";
	
	// Если данные о платформенной проверке не собраны, то ее надо исключить из состава правил проверки.
	Если ПравилаПлатформеннойПроверки.Количество() > 0 Тогда
		УсловиеПоПлатформеннойПроверке = "И ПравилаОбслуживаемыеТипы.Ссылка НЕ В(&МассивПравил)";
		ЗапросПоПравилам.УстановитьПараметр("МассивПравил", ПравилаПлатформеннойПроверки);
	КонецЕсли;
	
	ЗапросПоПравилам.Текст = СтрШаблон(ЗапросПоПравилам.Текст, УсловиеПоПлатформеннойПроверке);
	
	ТаблицаПравил = ЗапросПоПравилам.Выполнить().Выгрузить();
	
	ТаблицаПравил.Индексы.Добавить("ТипОбъекта");
	
	СтруктураОтбора = Новый Структура;
	
	Для Каждого ТипОбъекта Из Перечисления.ТипыОбъектов Цикл
		
		СтруктураОтбора.Вставить("ТипОбъекта", ТипОбъекта);
		
		ТаблицаПравилПоТипу = ТаблицаПравил.Скопировать(СтруктураОтбора);
		ТаблицаПравилПоТипу.Свернуть("Ссылка");
		
		МассивПравил = ТаблицаПравилПоТипу.ВыгрузитьКолонку("Ссылка");
		ПравилаПроверкиПоТипу.Вставить(ТипОбъекта, МассивПравил);
		
	КонецЦикла;
	
	Возврат ПравилаПроверкиПоТипу;
	
КонецФункции

// Подготавливает валидируемые ошибки и выполняет алгоритм валидации, заложенный в требование.
//
Процедура ВалидироватьПроверкуТребования(Требование)
	
	ЗапросПоАлгоритму = Новый Запрос;
	ЗапросПоАлгоритму.Текст = "
	|ВЫБРАТЬ
	|	Требования.АлгоритмВалидации
	|ИЗ
	|	Справочник.Требования КАК Требования
	|ГДЕ
	|	Требования.Ссылка = &Ссылка";
	
	ЗапросПоАлгоритму.УстановитьПараметр("Ссылка", Требование);
	
	ВыборкаАлгоритма = ЗапросПоАлгоритму.Выполнить().Выбрать();
	Если ВыборкаАлгоритма.Следующий() Тогда
		
		Попытка
			
			Выполнить(ВыборкаАлгоритма.АлгоритмВалидации);
			
		Исключение
			
			Событие = НСтр("ru='Алгоритм валидации'", Метаданные.ОсновнойЯзык.КодЯзыка);
			
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
			Сообщение = СтрШаблон(НСтр("ru='Не удалось выполнить алгоритм валидации по причине:
				|%1'"), ОписаниеОшибки);
			
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,, Сообщение);
			
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьОшибкиОбъектовПомеченныхНаУдаление(Расширение, ПроверкаВерсии, ЖурналПроверки)
	
	Версия = ПроверкаВерсии.Версия;
	Конфигурация = ПроверкаВерсии.Конфигурация;
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	// Удалим из ошибок помеченные на удаление объекты.
	ЗапросПоПомеченнымНаУдаление = Новый Запрос;
	ЗапросПоПомеченнымНаУдаление.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтруктураКонфигурации.Ссылка КАК Ссылка,
	|	СтруктураКонфигурации.Путь КАК Путь
	|ИЗ
	|	РегистрСведений.НайденныеОшибки КАК НайденныеОшибки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтруктураКонфигурации КАК СтруктураКонфигурации
	|		ПО НайденныеОшибки.Объект = СтруктураКонфигурации.Ссылка
	|ГДЕ
	|	СтруктураКонфигурации.Владелец = &Владелец
	|	И СтруктураКонфигурации.ПометкаУдаления
	|	И СтруктураКонфигурации.Расширение = &Расширение";
	
	ЗапросПоПомеченнымНаУдаление.УстановитьПараметр("Расширение", Расширение);
	ЗапросПоПомеченнымНаУдаление.УстановитьПараметр("Владелец", Версия);
	
	РезультатЗапроса = ЗапросПоПомеченнымНаУдаление.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Текст = НСтр("ru='Начало очистки ошибок объектов %1, помеченных на удаление'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Информация, Текст, ЖурналПроверки);
	
	ТекстОшибки = "";
	МассивОбъектов = Новый Массив;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
		НаборОшибок = РегистрыСведений.НайденныеОшибки.СоздатьНаборЗаписей();
		НаборОшибок.Отбор.Объект.Установить(Выборка.Ссылка);
		
		Попытка
			НаборОшибок.Записать();
		Исключение
			
			// Собираем объекты для записи ошибки в ЖР.
			Если МассивОбъектов.Количество() < 3 Тогда
				МассивОбъектов.Добавить(Выборка.Путь);
			КонецЕсли;
			
			Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
				Продолжить;
			КонецЕсли;
			
			// Пишем ошибку для первого объекта.
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ТекстОшибки = СтрШаблон(НСтр("ru='Не удалось очистить ошибки объектов, помеченных на удаление, по причине:%1%2'"),
				Символы.ПС,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ПустаяСтрока(ТекстОшибки) Тогда
		Текст = НСтр("ru='Очистка ошибок объектов %1, помеченных на удаление, завершена'");
		Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
		Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Информация, Текст, ЖурналПроверки);
	Иначе
		ТекстОшибки = ТекстОшибки + СтрШаблон(НСтр("ru='%1для объектов:%1%2%1и др. Проверка будет продолжена'"),
			Символы.ПС,
			СтрСоединить(МассивОбъектов, Символы.ПС));
		
		Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Ошибка, ТекстОшибки, ЖурналПроверки);
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьОшибкиИсключенныхОбъектов(Расширение, ПроверкаВерсии, ЖурналПроверки)
	
	Версия = ПроверкаВерсии.Версия;
	Конфигурация = ПроверкаВерсии.Конфигурация;
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	// Удалим из ошибок исключенные объекты.
	ЗапросПоИсключеннымОбъектам = Новый Запрос;
	ЗапросПоИсключеннымОбъектам.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтруктураКонфигурации.Ссылка КАК Ссылка,
	|	СтруктураКонфигурации.Путь КАК Путь
	|ИЗ
	|	РегистрСведений.НайденныеОшибки КАК НайденныеОшибки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтруктураКонфигурации КАК СтруктураКонфигурации
	|		ПО НайденныеОшибки.Объект = СтруктураКонфигурации.Ссылка
	|ГДЕ
	|	СтруктураКонфигурации.Владелец = &Владелец
	|	И НЕ СтруктураКонфигурации.СобраныСведения
	|	И СтруктураКонфигурации.Расширение = &Расширение";
	
	ЗапросПоИсключеннымОбъектам.УстановитьПараметр("Расширение", Расширение);
	ЗапросПоИсключеннымОбъектам.УстановитьПараметр("Владелец", Версия);
	
	РезультатЗапроса = ЗапросПоИсключеннымОбъектам.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Текст = НСтр("ru='Начало очистки ошибок исключенных объектов %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Информация, Текст, ЖурналПроверки);
	
	ТекстОшибки = "";
	МассивОбъектов = Новый Массив;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
		НаборОшибок = РегистрыСведений.НайденныеОшибки.СоздатьНаборЗаписей();
		НаборОшибок.Отбор.Объект.Установить(Выборка.Ссылка);
		
		Попытка
			НаборОшибок.Записать();
		Исключение
			
			// Собираем объекты для записи ошибки в ЖР.
			Если МассивОбъектов.Количество() < 3 Тогда
				МассивОбъектов.Добавить(Выборка.Путь);
			КонецЕсли;
			
			Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
				Продолжить;
			КонецЕсли;
			
			// Пишем ошибку для первого объекта.
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ТекстОшибки = СтрШаблон(НСтр("ru='Не удалось очистить ошибки исключенных объектов по причине:%1%2'"),
				Символы.ПС,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ПустаяСтрока(ТекстОшибки) Тогда
		Текст = НСтр("ru='Очистка ошибок исключенных объектов %1 завершена'");
		Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
		Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Информация, Текст, ЖурналПроверки);
	Иначе
		ТекстОшибки = ТекстОшибки + СтрШаблон(НСтр("ru='%1для объектов:%1%2%1и др. Проверка будет продолжена'"),
			Символы.ПС,
			СтрСоединить(МассивОбъектов, Символы.ПС));
		
		Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Ошибка, ТекстОшибки, ЖурналПроверки);
	КонецЕсли;
	
КонецПроцедуры

Процедура СинхронизироватьОшибкиИОсобенности(Расширение, ПроверкаВерсии, ПараметрыПроведения, ЖурналПроверки)
	
	Версия = ПроверкаВерсии.Версия;
	Конфигурация = ПроверкаВерсии.Конфигурация;
	
	Дата = ПроверкаВерсии.Дата;
	Номер = ПроверкаВерсии.Номер;
	
	НайденныеОшибки = ПараметрыПроведения.НайденныеОшибки;
	ПравилаПроверки = ПараметрыПроведения.ПравилаПроверки;
	ПравилаПлатформеннойПроверки = ПараметрыПроведения.ПравилаПлатформеннойПроверки;
	ПроверенныеОбъекты = ПараметрыПроведения.ПроверенныеОбъекты;
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	Текст = НСтр("ru='Начало синхронизации ошибок и особенностей %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Информация, Текст, ЖурналПроверки);
	
	ПустаяДата = Дата(1, 1, 1);
	СписокСвойствОсобенностей = "Состояние, ПричинаОсобенности, АвторОсобенности, ДатаПомещенияВОсобенности";
	
	ОсобенностиКонфигурацииИзРСНайденныеОшибки = ПолучитьОсобенностиКонфигурацииИзРСНайденныеОшибки(ПроверкаВерсии);
	ОсобенностиЭталоновКонфигурацииИзРСНайденныеОшибки =
		ПолучитьОсобенностиЭталоновКонфигурацииИзРСНайденныеОшибки(ПроверкаВерсии);
	
	ИсключенияБезПривязки = Конфигурация.ИсключенияБезПривязки;
	
	ТаблицаОшибокАПК = РегистрыСведений.НайденныеОшибки.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	
	НомерОбъекта = 0;
	ВсегоПроверенныхОбъектов = ПроверенныеОбъекты.Количество();
	
	// Определяем последний номер ошибки, записанный по диапазону данного документа.
	НомерОшибки = ПолучитьПоследнийНомерОшибки(Номер);
	
	НайденныеОшибки.Индексы.Добавить("Ключ");
	НайденныеОшибки.Индексы.Добавить("Объект");
	
	ТекущийПользовательСтрока = ПараметрыСеанса.ТекущийПользователь.Наименование;
	ТекущаяДата = ТекущаяДатаСеанса();
	ПричинаОсобенностиТехническийДолг = Справочники.ПричиныОсобенности.ТехническийДолг;
	СостояниеОсобенность = Перечисления.СостояниеОшибки.Особенность;
	СостояниеЗарегистрирована = Перечисления.СостояниеОшибки.Зарегистрирована;
	
	// Синхронизируем ошибки в регистре отдельно по каждому проверявшемуся объекту.
	Для Каждого ПроверенныйОбъект Из ПроверенныеОбъекты Цикл
		
		НомерОбъекта = НомерОбъекта + 1;
		ПроверенныйОбъектПуть = ПроверенныйОбъект.Путь;
		МассивОшибокИзРегистраДляУдаления = Новый Массив;
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		
		ТекстСостояния = НСтр("ru='Проанализировано ошибок по %1 объектам из %2'");
		ТекстСостояния = СтрШаблон(ТекстСостояния, НомерОбъекта, ВсегоПроверенныхОбъектов);
		Состояние(ТекстСостояния);
		#КонецЕсли
		
		НаборОшибокИзРегистра = РегистрыСведений.НайденныеОшибки.СоздатьНаборЗаписей();
		НаборОшибокИзРегистра.Отбор.Объект.Установить(ПроверенныйОбъект);
		НаборОшибокИзРегистра.Прочитать();
		Для Каждого ОшибкаИзРегистра Из НаборОшибокИзРегистра Цикл
			
			Если Дата < ОшибкаИзРегистра.ДатаМодификации Тогда
				Продолжить;
			КонецЕсли;
			
			ОшибкаИзРегистра.ДатаМодификации = Дата;
			
			ОшибкаИзРегистраПравило = ОшибкаИзРегистра.Правило;
			ОшибкаИзРегистраОшибка = ОшибкаИзРегистра.Ошибка;
			ОшибкаИзРегистраОшибкаКод = ОшибкаИзРегистраОшибка.Код;
			ОшибкаИзРегистраУточнение = ОшибкаИзРегистра.Уточнение;
			ОшибкаИзРегистраМестоОбнаружения = ОшибкаИзРегистра.МестоОбнаружения;
			
			// Делаем поиск ошибок из регистра в найденных ошибках по ключу.
			Ключ = СтрШаблон("%1.%2.%3.%4.%5",
				ПроверенныйОбъект.Код,
				ОшибкаИзРегистраПравило.Код,
				ОшибкаИзРегистраОшибкаКод,
				ОшибкаИзРегистраУточнение,
				ОшибкаИзРегистраМестоОбнаружения);
			
			Ключ = ВРег(Ключ);
			Ключ = УдалитьНезначащиеСимволы(Ключ);
			
			СтрокаСНовойОшибкой = НайденныеОшибки.Найти(Ключ, "Ключ");
			
			// Если ошибка из регистра при повторной проверке не обнаружена.
			Если СтрокаСНовойОшибкой = Неопределено Тогда
				
				// Если правило проверки не было выбрано при проверке, то оставляем ошибку в прежнем состоянии.
				Если НЕ МассивСодержитЭлемент(ПравилаПроверки, ОшибкаИзРегистраПравило) Тогда
					Продолжить;
				КонецЕсли;
				
				// Иначе - правило выбиралось, но ошибка не найдена - анализируем имеющиеся особенности.
				Если ОшибкаИзРегистра.Состояние = СостояниеОсобенность Тогда
					
					// Пропускаем особенности по платформенной проверке.
					Если МассивСодержитЭлемент(ПравилаПлатформеннойПроверки, ОшибкаИзРегистраПравило) Тогда
						Продолжить;
					КонецЕсли;
					
					// Пропускаем особенности с причиной "Технический долг".
					Если ОшибкаИзРегистра.ПричинаОсобенности = ПричинаОсобенностиТехническийДолг Тогда
						Продолжить;
					КонецЕсли;
					
				КонецЕсли;
				
				// Если ошибка из регистра не найдена, добавим ее в массив для удаления.
				МассивОшибокИзРегистраДляУдаления.Добавить(ОшибкаИзРегистра);
				
			Иначе
				
				// Иначе - ошибка из регистра найдена при повторной проверке - удалим ее из таблицы найденных ошибок.
				НайденныеОшибки.Удалить(СтрокаСНовойОшибкой);
				
				// Проверяем, является ли особенностью существующая ошибка в регистре ошибок.
				Если ОшибкаИзРегистра.Состояние = СостояниеОсобенность Тогда
					
					// Проверяем, заполнены ли все поля для особенности.
					Если ПустаяСтрока(ОшибкаИзРегистра.АвторОсобенности) Тогда
						// Если автор пустой, то перезаполняем его текущим пользователем.
						ОшибкаИзРегистра.АвторОсобенности = ТекущийПользовательСтрока;
					КонецЕсли;
					
					Если ОшибкаИзРегистра.ДатаПомещенияВОсобенности = ПустаяДата Тогда
						// Если дата особенности пустая, то перезаполняем ее текущей датой.
						ОшибкаИзРегистра.ДатаПомещенияВОсобенности = ТекущаяДата;
					КонецЕсли;
					
					// Пропускаем особенность.
					Продолжить;
					
				КонецЕсли;
				
				КлючОсобенности = СтрШаблон("%1.%2.%3",
					ПроверенныйОбъектПуть,
					ОшибкаИзРегистраОшибкаКод,
					ОшибкаИзРегистраУточнение);
				
				КлючОсобенности = УдалитьНезначащиеСимволы(КлючОсобенности);
				
				// Если исключения с привязкой, то добавляем в ключ место обнаружения.
				Если НЕ ИсключенияБезПривязки Тогда
					КлючОсобенности = КлючОсобенности + "." + ОшибкаИзРегистраМестоОбнаружения;
				КонецЕсли;
				
				КлючОсобенности = УдалитьНезначащиеСимволы(КлючОсобенности);
				
				// Искать среди особенностей из РС "Найденные ошибок" по текущей конфигурации не имеет смысла,
				// т.к. проверяем эти же ошибки из регистра.
				// Поэтому сразу ищем ошибку в РС "Найденные ошибок" по эталонам конфигурации.
				СуществующаяОсобенность = ОсобенностиЭталоновКонфигурацииИзРСНайденныеОшибки[КлючОсобенности];
				Если СуществующаяОсобенность <> Неопределено Тогда
					// Если ошибка является особенностью, то перебиваем значения свойств особенности из РС "Найденные ошибки".
					ЗаполнитьЗначенияСвойств(ОшибкаИзРегистра, СуществующаяОсобенность, СписокСвойствОсобенностей);
					Продолжить;
				КонецЕсли;
				
				// Если ошибка не особенность, то возвращаем состояние "Зарегистрирована".
				// Например, у ошибки могли установить состояние "Исправлена", но не исправить ее в конфигурации.
				ОшибкаИзРегистра.Состояние = Перечисления.СостояниеОшибки.Зарегистрирована;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если МассивОшибокИзРегистраДляУдаления.Количество() > 0 Тогда
			// Удалим из регистра ошибки, которые не были найдены при повторной проверке.
			Для Каждого УдаляемаяОшибка Из МассивОшибокИзРегистраДляУдаления Цикл
				НаборОшибокИзРегистра.Удалить(УдаляемаяОшибка);
			КонецЦикла;
			
			// Если по этому объекту больше нет ошибок, то запишем результат сразу в регистр.
			// Смысл в следующем: если новых ошибок по этому объекту нет, то позже при записи ТаблицаОшибокАПК
			// будет работать отбор по объекту, а т.к. ошибок по объекту в ТаблицаОшибокАПК нет,
			// то и ненайденные ошибки останутся в регистре.
			// Поэтому приходится предварительно очищать и перезаписывать НаборОшибокИзРегистра.
			Если НаборОшибокИзРегистра.Количество() = 0 Тогда
				// В данном случае удаление комментариев ошибок происходит в процедуре УдалитьКомментарийОшибки(),
				// которая вызывается в обработчике перед записью набора записей РС "Найденные ошибки",
				// поэтому вызывать отдельно удаление комментариев не требуется.
				НаборОшибокИзРегистра.Записать();
			Иначе
				// Если удаляются не все ошибки по объекту, то напрямую удаляем комментарии только для удаляемых ошибок.
				УдалитьКомментарииУдаляемыхОшибок(ПроверенныйОбъект, МассивОшибокИзРегистраДляУдаления);
			КонецЕсли;
		КонецЕсли;
		
		// Оставшиеся найденные ошибки по объекту необходимо добавить как новые.
		ОтборПоОбъекту = Новый Структура;
		ОтборПоОбъекту.Вставить("Объект", ПроверенныйОбъект);
		НовыеОшибки = НайденныеОшибки.НайтиСтроки(ОтборПоОбъекту);
		
		Для Каждого НоваяОшибка Из НовыеОшибки Цикл
			
			НомерОшибки = НомерОшибки + 1;
			
			ДобавленнаяОшибка = НаборОшибокИзРегистра.Добавить();
			
			ЗаполнитьЗначенияСвойств(ДобавленнаяОшибка, НоваяОшибка, "Объект, Правило, Ошибка, Уточнение, МестоОбнаружения");
			ДобавленнаяОшибка.Номер = НомерОшибки;
			ДобавленнаяОшибка.ДатаМодификации = Дата;
			
			КлючОсобенности = СтрШаблон("%1.%2.%3",
				ПроверенныйОбъектПуть,
				НоваяОшибка.Ошибка.Код,
				НоваяОшибка.Уточнение);
			
			КлючОсобенности = УдалитьНезначащиеСимволы(КлючОсобенности);
			
			// Если исключения с привязкой, то добавляем в ключ место обнаружения.
			Если НЕ ИсключенияБезПривязки Тогда
				КлючОсобенности = КлючОсобенности + "." + НоваяОшибка.МестоОбнаружения;
			КонецЕсли;
			
			КлючОсобенности = УдалитьНезначащиеСимволы(КлючОсобенности);
			
			// Ищем ошибку в особенностях из РС "Найденные ошибки" по текущей конфигурации.
			СуществующаяОсобенность = ОсобенностиКонфигурацииИзРСНайденныеОшибки[КлючОсобенности];
			Если СуществующаяОсобенность <> Неопределено Тогда
				// Если ошибка является особенностью, то заполняем значения свойств особенности из существующей особенности.
				ЗаполнитьЗначенияСвойств(ДобавленнаяОшибка, СуществующаяОсобенность, СписокСвойствОсобенностей);
				Продолжить;
			КонецЕсли;
			
			// Ищем ошибку в особенностях из РС "Найденные ошибки" по эталонам конфигурации.
			СуществующаяОсобенность = ОсобенностиЭталоновКонфигурацииИзРСНайденныеОшибки[КлючОсобенности];
			Если СуществующаяОсобенность <> Неопределено Тогда
				// Если ошибка является особенностью, то заполняем значения свойств особенности из существующей особенности.
				ЗаполнитьЗначенияСвойств(ДобавленнаяОшибка, СуществующаяОсобенность, СписокСвойствОсобенностей);
				Продолжить;
			КонецЕсли;
			
			ДобавленнаяОшибка.Состояние = СостояниеЗарегистрирована;
			
		КонецЦикла;
		
		Для Каждого ТекущаяЗапись Из НаборОшибокИзРегистра Цикл
			НоваяСтрока = ТаблицаОшибокАПК.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяЗапись);
		КонецЦикла;
		
	КонецЦикла;
	
	ПараметрыПроведения.НайденныеОшибки = Неопределено;
	ОсобенностиКонфигурацииИзРСОсобенности = Неопределено;
	ОсобенностиКонфигурацииИзРСНайденныеОшибки = Неопределено;
	ОсобенностиЭталоновКонфигурацииИзРСНайденныеОшибки = Неопределено;
	
	Текст = НСтр("ru='Синхронизация ошибок и особенностей %1 завершена'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Информация, Текст, ЖурналПроверки);
	
	ПараметрыПроведения.Вставить("ТаблицаОшибокАПК", ТаблицаОшибокАПК);
	
КонецПроцедуры

Функция ПолучитьОсобенностиКонфигурацииИзРСНайденныеОшибки(ПроверкаВерсии)
	
	Особенности = Новый Соответствие;
	
	ЗапросПоОсобенностям = Новый Запрос;
	ЗапросПоОсобенностям.Текст = "
	|ВЫБРАТЬ
	|	НайденныеОшибки.Объект.Путь КАК Объект,
	|	НайденныеОшибки.Ошибка.Код КАК Ошибка,
	|	НайденныеОшибки.Уточнение,
	|	НайденныеОшибки.МестоОбнаружения,
	|	НайденныеОшибки.Состояние,
	|	НайденныеОшибки.ПричинаОсобенности,
	|	НайденныеОшибки.АвторОсобенности,
	|	НайденныеОшибки.ДатаПомещенияВОсобенности
	|ИЗ
	|	РегистрСведений.НайденныеОшибки КАК НайденныеОшибки
	|ГДЕ
	|	НайденныеОшибки.Объект.Владелец = &Версия
	|	И НайденныеОшибки.Состояние = &Состояние";
	
	ЗапросПоОсобенностям.УстановитьПараметр("Версия", ПроверкаВерсии.Эталон);
	ЗапросПоОсобенностям.УстановитьПараметр("Состояние", Перечисления.СостояниеОшибки.Особенность);
	
	ТаблицаОсобенностей = ЗапросПоОсобенностям.Выполнить().Выгрузить();
	Если ТаблицаОсобенностей.Количество() = 0 Тогда
		Возврат Особенности;
	КонецЕсли;
	
	ИсключенияБезПривязки = ПроверкаВерсии.Конфигурация.ИсключенияБезПривязки;
	
	Для Каждого СтрокаОсобенности Из ТаблицаОсобенностей Цикл
		
		Ключ = СтрокаОсобенности.Объект + "." + СтрокаОсобенности.Ошибка + "." + СтрокаОсобенности.Уточнение;
		Если НЕ ИсключенияБезПривязки Тогда
			Ключ = Ключ + "." + СтрокаОсобенности.МестоОбнаружения;
		КонецЕсли;
		
		Ключ = УдалитьНезначащиеСимволы(Ключ);
		
		Особенности.Вставить(Ключ, СтрокаОсобенности);
		
	КонецЦикла;
	
	Возврат Особенности;
	
КонецФункции

Функция ПолучитьОсобенностиЭталоновКонфигурацииИзРСНайденныеОшибки(ПроверкаВерсии)
	
	Конфигурация = ПроверкаВерсии.Конфигурация;
	
	Особенности = Новый Соответствие;
	
	// Получаем версии эталонов библиотечных конфигураций.
	Эталоны = Новый Массив;
	Для Каждого БиблиотечнаяКонфигурация Из Конфигурация.Библиотеки Цикл
		ВерсияЭталон = НайтиПоследнююВерсию(БиблиотечнаяКонфигурация.БиблиотечнаяКонфигурация);
		Если ЗначениеЗаполнено(ВерсияЭталон) Тогда
			Эталоны.Добавить(ВерсияЭталон);
		КонецЕсли;
	КонецЦикла;
	
	Если Эталоны.Количество() = 0 Тогда
		Возврат Особенности;
	КонецЕсли;
	
	ЗапросПоОсобенностям = Новый Запрос;
	ЗапросПоОсобенностям.Текст = "
	|ВЫБРАТЬ
	|	НайденныеОшибки.Объект.Путь КАК Объект,
	|	НайденныеОшибки.Ошибка.Код КАК Ошибка,
	|	НайденныеОшибки.Уточнение,
	|	НайденныеОшибки.МестоОбнаружения,
	|	НайденныеОшибки.Состояние,
	|	НайденныеОшибки.ПричинаОсобенности,
	|	НайденныеОшибки.АвторОсобенности,
	|	НайденныеОшибки.ДатаПомещенияВОсобенности
	|ИЗ
	|	РегистрСведений.НайденныеОшибки КАК НайденныеОшибки
	|ГДЕ
	|	НайденныеОшибки.Объект.Владелец В (&ВерсииЭталонов)
	|	И НайденныеОшибки.Состояние = &Состояние
	|
	|// Необходима сортировка по возрастанию даты особенности, чтобы значение соответствия было с максимальной датой.
	|УПОРЯДОЧИТЬ ПО
	|	НайденныеОшибки.ДатаПомещенияВОсобенности ВОЗР";
	
	ЗапросПоОсобенностям.УстановитьПараметр("ВерсииЭталонов", Эталоны);
	ЗапросПоОсобенностям.УстановитьПараметр("Состояние", Перечисления.СостояниеОшибки.Особенность);
	
	ТаблицаОсобенностей = ЗапросПоОсобенностям.Выполнить().Выгрузить();
	Если ТаблицаОсобенностей.Количество() = 0 Тогда
		Возврат Особенности;
	КонецЕсли;
	
	ИсключенияБезПривязки = Конфигурация.ИсключенияБезПривязки;
	
	Для Каждого СтрокаОсобенности Из ТаблицаОсобенностей Цикл
		
		Ключ = СтрокаОсобенности.Объект + "." + СтрокаОсобенности.Ошибка + "." + СтрокаОсобенности.Уточнение;
		Если НЕ ИсключенияБезПривязки Тогда
			Ключ = Ключ + "." + СтрокаОсобенности.МестоОбнаружения;
		КонецЕсли;
		
		Ключ = УдалитьНезначащиеСимволы(Ключ);
		
		Особенности.Вставить(Ключ, СтрокаОсобенности);
		
	КонецЦикла;
	
	Возврат Особенности;
	
КонецФункции

Процедура УдалитьКомментарииУдаляемыхОшибок(ПроверенныйОбъект, МассивУдаляемыхОшибокОбъекта)
	
	НаборЗаписей = РегистрыСведений.КомментарииНайденныхОшибок.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(ПроверенныйОбъект);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаЗаписей = НаборЗаписей.Выгрузить();
	
	Для Каждого ЗаписьМассива Из МассивУдаляемыхОшибокОбъекта Цикл
		
		НайденнаяСтрока = ТаблицаЗаписей.Найти(ЗаписьМассива.Номер, "Номер");
		Если НайденнаяСтрока <> Неопределено Тогда
			ТаблицаЗаписей.Удалить(НайденнаяСтрока);
		КонецЕсли;
		
	КонецЦикла;
	
	НаборЗаписей.Загрузить(ТаблицаЗаписей);
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция ПолучитьПоследнийНомерОшибки(Номер)
	
	Множитель = 100000000000;
	
	ЗапросПоНомеру = Новый Запрос;
	ЗапросПоНомеру.Текст = "
	|ВЫБРАТЬ
	|	МАКСИМУМ(НайденныеОшибки.Номер) КАК МаксимальныйНомерОшибки
	|ИЗ
	|	РегистрСведений.НайденныеОшибки КАК НайденныеОшибки
	|ГДЕ
	|	НайденныеОшибки.Номер МЕЖДУ &МинимальныйНомер И &МаксимальныйНомер";
	
	ЗапросПоНомеру.УстановитьПараметр("МинимальныйНомер", Номер * Множитель);
	ЗапросПоНомеру.УстановитьПараметр("МаксимальныйНомер", (Номер + 1) * Множитель);
	
	ВыборкаНомера = ЗапросПоНомеру.Выполнить().Выбрать();
	Если ВыборкаНомера.Следующий() Тогда
		Если ЗначениеЗаполнено(ВыборкаНомера.МаксимальныйНомерОшибки) Тогда
			НомерОшибки = ВыборкаНомера.МаксимальныйНомерОшибки + 1;
		Иначе
			НомерОшибки = Номер * Множитель + 1;
		КонецЕсли;
		
	Иначе
		НомерОшибки = Номер * Множитель + 1;
	КонецЕсли;
	
	Возврат НомерОшибки;
	
КонецФункции

Процедура ЗарегистрироватьВсеОшибкиКакОсобенности(Расширение, ПроверкаВерсии, ПараметрыПроведения, ЖурналПроверки)
	
	ТаблицаОшибокАПК = ПараметрыПроведения.ТаблицаОшибокАПК;
	Конфигурация = ПроверкаВерсии.Конфигурация;
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	КоличествоОшибок = ТаблицаОшибокАПК.Количество();
	
	Текст = НСтр("ru='Начало регистрации особенностей %1 (%2)'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения, КоличествоОшибок);
	Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Информация, Текст, ЖурналПроверки);
	
	СостояниеОсобенность = Перечисления.СостояниеОшибки.Особенность;
	ПричинаОсобенностиТехническийДолг = Справочники.ПричиныОсобенности.ТехническийДолг;
	ТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
	ТекущаяДата = ТекущаяДатаСеанса();
	
	НачатьТранзакцию();
	
	Попытка
		
		СтруктураОтбора = Новый Структура;
		
		Счетчик = 0;
		ТекстСостоянияШаблон = НСтр("ru='Выполняется регистрация особенностей. Зарегистрировано %1 из %2'");
		
		Для Каждого СтрокаОшибки Из ТаблицаОшибокАПК Цикл
			
			#Если Клиент Тогда
			ОбработкаПрерыванияПользователя();
			
			Счетчик = Счетчик + 1;
			ТекстСостояния = СтрШаблон(ТекстСостоянияШаблон, Счетчик, КоличествоОшибок);
			Состояние(ТекстСостояния);
			#КонецЕсли
			
			// Если состояние ошибки особенность, то пропускаем.
			Если СтрокаОшибки.Состояние = СостояниеОсобенность Тогда
				Продолжить;
			КонецЕсли;
			
			// Меняем состояние ошибки и заполняем необходимые поля.
			СтрокаОшибки.Состояние = СостояниеОсобенность;
			СтрокаОшибки.ПричинаОсобенности = ПричинаОсобенностиТехническийДолг;
			СтрокаОшибки.АвторОсобенности = Строка(ТекущийПользователь);
			СтрокаОшибки.ДатаПомещенияВОсобенности = ТекущаяДата;
			
		КонецЦикла;
		
		Текст = НСтр("ru='Регистрация особенностей %1 завершена'");
		Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
		Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Информация, Текст, ЖурналПроверки);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		
		ТекстОшибки = НСтр("ru='Не удалось зарегистрировать особенности %1 по причине:%2%3'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, ОписаниеКонфигурацииИлиРасширения, Символы.ПС, ОписаниеОшибки);
		
		Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Ошибка, ОписаниеОшибки, ЖурналПроверки);
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ДобавитьОшибкиЗаКонфигурацию(Расширение, ПроверкаВерсии, ПараметрыПроведения)
	
	Версия = ПроверкаВерсии.Версия;
	Дата = ПроверкаВерсии.Дата;
	
	ТаблицаОшибокАПК = ПараметрыПроведения.ТаблицаОшибокАПК;
	СоответствиеОшибок = ПараметрыПроведения.СоответствиеОшибок;
	МаксимальноеКоличествоОшибокОдногоВида = ПараметрыПроведения.МаксимальноеКоличествоОшибокОдногоВида;
	
	Если ТаблицаОшибокАПК.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаОшибокАПК.Сортировать("Номер Убыв");
	НомерОшибки = ТаблицаОшибокАПК[0].Номер + 1;
	
	ОбъектКонфигурация = ПолучитьЭлементСтруктурыМетаданных(Версия,,, Перечисления.ТипыОбъектов.Конфигурация, Расширение);
	
	Уточнение = НСтр("ru='Обнаружено %1 ошибок данного вида. В отчете отражено %2 ошибок.'");
	
	// Получим количество реальных ошибок из таблицы без учета особенностей.
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Состояние", Перечисления.СостояниеОшибки.Зарегистрирована);
	
	// Проверим, если все ошибки были особенностями, то выходим.
	ТаблицаВидовОшибок = ТаблицаОшибокАПК.Скопировать(СтруктураОтбора, "Ошибка");
	Если ТаблицаВидовОшибок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаВидовОшибок.Колонки.Добавить("Количество");
	ТаблицаВидовОшибок.ЗаполнитьЗначения(1, "Количество");
	ТаблицаВидовОшибок.Свернуть("Ошибка", "Количество");
	ТаблицаВидовОшибок.Индексы.Добавить("Ошибка");
	
	Для Каждого СтрокаСоответствия Из СоответствиеОшибок Цикл
		
		// Проверим количество ошибок при регистрации.
		КоличествоОшибок = СтрокаСоответствия.Значение;
		Если КоличествоОшибок <= МаксимальноеКоличествоОшибокОдногоВида Тогда
			Продолжить;
		КонецЕсли;
		
		// Ключ состоит из кода правила и кода ошибки через перенос строки.
		МассивКодов = СтрРазделить(СтрокаСоответствия.Ключ, Символы.ПС);
		Правило = Справочники.Правила.НайтиПоКоду(МассивКодов[0]);
		Ошибка = Справочники.ОбнаруживаемыеОшибки.НайтиПоКоду(МассивКодов[1]);
		
		// Некоторые ошибки могут оказаться особенностями, поэтому перепроверим количество реальных ошибок из таблицы.
		СтрокаОшибки = ТаблицаВидовОшибок.Найти(Ошибка, "Ошибка");
		Если СтрокаОшибки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// Если количество зарегистрированных ошибок без особенностей меньше максимального, то пропускаем этот вид ошибок.
		Если СтрокаОшибки.Количество < МаксимальноеКоличествоОшибокОдногоВида Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяОшибка = ТаблицаОшибокАПК.Добавить();
		НоваяОшибка.Объект = ОбъектКонфигурация;
		НоваяОшибка.Правило = Правило;
		НоваяОшибка.Номер = НомерОшибки;
		НоваяОшибка.Ошибка = Ошибка;
		НоваяОшибка.Состояние = Перечисления.СостояниеОшибки.Зарегистрирована;
		НоваяОшибка.МестоОбнаружения = "";
		НоваяОшибка.Уточнение = СтрШаблон(Уточнение, КоличествоОшибок, МаксимальноеКоличествоОшибокОдногоВида);
		НоваяОшибка.ДатаМодификации = Дата;
		
		НомерОшибки = НомерОшибки + 1;
		
	КонецЦикла;
	
КонецПроцедуры

// Записывает связанные записи регистра сведений НайденныеОшибки.
//
Процедура ЗаписатьОшибкиАПК(Расширение, ПроверкаВерсии, ПараметрыПроведения, ЖурналПроверки)
	
	Конфигурация = ПроверкаВерсии.Конфигурация;
	
	ТаблицаОшибокАПК = ПараметрыПроведения.ТаблицаОшибокАПК;
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	Текст = НСтр("ru='Начало записи ошибок %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Информация, Текст, ЖурналПроверки);
	
	ТаблицаОбъектов = ТаблицаОшибокАПК.Скопировать(, "Объект");
	ТаблицаОбъектов.Свернуть("Объект", "");
	
	НомерОбъекта = 0;
	ВсегоОбъектов = ТаблицаОбъектов.Количество();
	
	КоличествоНезаписанныхОбъектов = 0;
	
	ДатаПроверки = Формат(ТекущаяДатаСеанса(), "ДФ=""ггггММдд""");
	ИмяСобытия = СтрШаблон(НСтр("ru='Ошибка при записи найденных ошибок.%1.%2'", Метаданные.ОсновнойЯзык.КодЯзыка),
		СтрЗаменить(Конфигурация.Наименование, ".", "_"), ДатаПроверки);
	
	СтруктураОтбора = Новый Структура;
	ТаблицаОшибокАПК.Индексы.Добавить("Объект");
	
	Для Каждого СтрокаОбъекта Из ТаблицаОбъектов Цикл
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		#КонецЕсли
		
		ОбъектСсылка = СтрокаОбъекта.Объект;
		
		СтруктураОтбора.Вставить("Объект", ОбъектСсылка);
		СтрокиОшибок = ТаблицаОшибокАПК.НайтиСтроки(СтруктураОтбора);
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НайденныеОшибки");
			ЭлементБлокировки.УстановитьЗначение("Объект", ОбъектСсылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			
			Блокировка.Заблокировать();
			
			НаборОшибок = РегистрыСведений.НайденныеОшибки.СоздатьНаборЗаписей();
			НаборОшибок.Отбор.Объект.Установить(ОбъектСсылка);
			
			Для Каждого СтрокаОшибки Из СтрокиОшибок Цикл
				Запись = НаборОшибок.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, СтрокаОшибки);
			КонецЦикла;
			
			СчетчикПопыток = 10;
			Пока СчетчикПопыток > 0 Цикл
				Попытка
					НаборОшибок.Записать(Истина);
					Прервать;
				Исключение
					СчетчикПопыток = СчетчикПопыток  - 1;
				КонецПопытки;
			КонецЦикла;
			
			НомерОбъекта = НомерОбъекта + 1;
			
			#Если Клиент Тогда
			ТекстСостояния = НСтр("ru='Записаны ошибки для объекта №%1 из %2 - %3'");
			ТекстСостояния = СтрШаблон(ТекстСостояния, НомерОбъекта, ВсегоОбъектов, ОбъектСсылка.Путь);
			Состояние(ТекстСостояния);
			#КонецЕсли
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			
			КоличествоНезаписанныхОбъектов = КоличествоНезаписанныхОбъектов + 1;
			
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
			Сообщение = СтрШаблон(НСтр("ru='Не удалось записать ошибки объекта %1 по причине:
				|%2'"), ОбъектСсылка.Путь, ОписаниеОшибки);
			
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, Сообщение);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Текст = НСтр("ru='Запись ошибок %1 завершена'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Информация, Текст, ЖурналПроверки);
	
	Если КоличествоНезаписанныхОбъектов > 0 Тогда
		Текст = НСтр("ru='Не удалось записать ошибки %1 объекта(ов). Подробности см. в журнале регистрации'");
		Текст = СтрШаблон(Текст, КоличествоНезаписанныхОбъектов);
		Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Информация, Текст, ЖурналПроверки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеренестиКомментарии(Расширение, ПроверкаВерсии, ПараметрыПроведения, ЖурналПроверки)
	
	Конфигурация = ПроверкаВерсии.Конфигурация;
	
	ТаблицаОшибокАПК = ПараметрыПроведения.ТаблицаОшибокАПК;
	
	ОписаниеКонфигурацииИлиРасширения = ПолучитьОписаниеКонфигурацииИлиРасширения(Расширение);
	
	ЗапросПоКомментариям = Новый Запрос;
	ЗапросПоКомментариям.Текст = "
	|ВЫБРАТЬ
	|	НайденныеОшибки.Номер КАК Номер,
	|	НайденныеОшибки.Объект.Путь КАК Путь,
	|	НайденныеОшибки.Правило КАК Правило,
	|	НайденныеОшибки.Ошибка КАК Ошибка,
	|	НайденныеОшибки.Уточнение КАК Уточнение,
	|	НайденныеОшибки.МестоОбнаружения КАК МестоОбнаружения,
	|	НайденныеОшибки.ДатаМодификации КАК ДатаМодификации,
	|	КомментарииНайденныхОшибок.Комментарий КАК Комментарий
	|ИЗ
	|	РегистрСведений.НайденныеОшибки КАК НайденныеОшибки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КомментарииНайденныхОшибок КАК КомментарииНайденныхОшибок
	|		ПО НайденныеОшибки.Номер = КомментарииНайденныхОшибок.Номер
	|ГДЕ
	|	НайденныеОшибки.Объект.Владелец.Владелец = &Конфигурация
	|	И НайденныеОшибки.Объект.Расширение = &Расширение";
	
	ЗапросПоКомментариям.УстановитьПараметр("Конфигурация", Конфигурация);
	ЗапросПоКомментариям.УстановитьПараметр("Расширение", Расширение);
	
	ТаблицаКомментариев = ЗапросПоКомментариям.Выполнить().Выгрузить();
	
	Если ТаблицаКомментариев.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Текст = НСтр("ru='Начало переноса комментариев к ошибкам %1'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Информация, Текст, ЖурналПроверки);
	
	ТаблицаКомментариев.Индексы.Добавить("Номер");
	ТаблицаКомментариев.Индексы.Добавить("Путь, Правило, Ошибка, Уточнение, МестоОбнаружения");
	
	СтруктураОтбора = Новый Структура("Путь, Правило, Ошибка, Уточнение, МестоОбнаружения");
	
	ТекстСостоянияШаблон = НСтр("ru='Выполняется перенос комментариев к найденным ошибкам. Обработано %1 из %2 ошибок'");
	
	СчетчикОшибок = 0;
	КоличествоОшибок = ТаблицаОшибокАПК.Количество();
	Для Каждого ОшибкаАПК Из ТаблицаОшибокАПК Цикл
		
		НомерОшибки = ОшибкаАПК.Номер;
		ОбъектКонфигурации = ОшибкаАПК.Объект;
		
		#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
		
		СчетчикОшибок = СчетчикОшибок + 1;
		Состояние(СтрШаблон(ТекстСостоянияШаблон, СчетчикОшибок, КоличествоОшибок));
		#КонецЕсли
		
		// У этой ошибки уже есть комментарий, переносить не нужно.
		Если ТаблицаКомментариев.Найти(НомерОшибки, "Номер") <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// Ошибку не нашли по номеру, ищем похожие ошибки по различным полям.
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, ОшибкаАПК);
		СтруктураОтбора.Путь = ОбъектКонфигурации.Путь;
		ТаблицаКомментариевКопия = ТаблицаКомментариев.Скопировать(СтруктураОтбора, "ДатаМодификации, Комментарий");
		
		КоличествоКомментариев = ТаблицаКомментариевКопия.Количество();
		Если КоличествоКомментариев = 0 Тогда
			// Похожих ошибок по различным полям не нашлось, комментариев нет.
			Продолжить;
		КонецЕсли;
		
		Если КоличествоКомментариев > 1 Тогда
			// Сортируем комментарии по дате модификации ошибки, чтобы взять самый свежий комментарий.
			ТаблицаКомментариевКопия.Сортировать("ДатаМодификации УБЫВ");
		КонецЕсли;
		
		Комментарий = ТаблицаКомментариевКопия[0].Комментарий;
		
		// Добавляем новый комментарий в регистр.
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.КомментарииНайденныхОшибок");
		ЭлементБлокировки.УстановитьЗначение("Объект", ОбъектКонфигурации);
		ЭлементБлокировки.УстановитьЗначение("Номер", НомерОшибки);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка.Заблокировать();
			
			КомментарииНайденныхОшибокНаборЗаписей = РегистрыСведений.КомментарииНайденныхОшибок.СоздатьНаборЗаписей();
			КомментарииНайденныхОшибокНаборЗаписей.Отбор.Объект.Установить(ОбъектКонфигурации);
			КомментарииНайденныхОшибокНаборЗаписей.Отбор.Номер.Установить(НомерОшибки);
			
			НоваяЗапись = КомментарииНайденныхОшибокНаборЗаписей.Добавить();
			НоваяЗапись.Номер = НомерОшибки;
			НоваяЗапись.Объект = ОбъектКонфигурации;
			НоваяЗапись.Комментарий = Комментарий;
			
			КомментарииНайденныхОшибокНаборЗаписей.Записать();
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			Сообщить(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЦикла;
	
	Текст = НСтр("ru='Перенос комментариев к ошибкам %1 завершен'");
	Текст = СтрШаблон(Текст, ОписаниеКонфигурацииИлиРасширения);
	Зафиксировать(Конфигурация.Наименование, УровеньЖурналаРегистрации.Информация, Текст, ЖурналПроверки);
	
	#Если Клиент Тогда
	Состояние();
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаОрфографии

// Заполняет соответствие верных слов из словаря и регистра сведений "Верные слова":
//  ВерныеСлова - соответствие правильных слов,
//  ДополнитьИзРегистра - флаг, добавлять слова из регистра сведений.
//
Процедура ИнициализироватьСловарьВерныхСлов(ВерныеСлова, ДополнитьИзРегистра = Истина) Экспорт
	
	Если ТипЗнч(ВерныеСлова) = Тип("Соответствие") Тогда
		Возврат;
	КонецЕсли;
	
	// Заполняем словарь из макета.
	ИмяФайлаСловаря = ПолучитьИмяВременногоФайла("txt");
	ПолучитьОбщийМакет("СловарьВерныхСлов").Записать(ИмяФайлаСловаря);
	
	ВерныеСлова = Новый Соответствие;
	
	МакетСловаря = Новый ЧтениеТекста(ИмяФайлаСловаря, "windows-1251");
	Слово = МакетСловаря.ПрочитатьСтроку();
	Пока Слово <> Неопределено Цикл
		ВерныеСлова.Вставить(Слово, Истина);
		Слово = МакетСловаря.ПрочитатьСтроку();
	КонецЦикла;
	
	ФайлУдалить(ИмяФайлаСловаря);
	
	Если НЕ ДополнитьИзРегистра Тогда
		Возврат;
	КонецЕсли;
	
	// Внутренний словарь.
	ЗапросПоСловарю = Новый Запрос;
	ЗапросПоСловарю.Текст = "
	|ВЫБРАТЬ
	|	ВерныеСлова.Слово КАК Слово
	|ИЗ
	|	РегистрСведений.ВерныеСлова КАК ВерныеСлова";
	
	ВыборкаСлов = ЗапросПоСловарю.Выполнить().Выбрать();
	Пока ВыборкаСлов.Следующий() Цикл
		ВерныеСлова.Вставить(СокрЛП(ВРег(ВыборкаСлов.Слово)), Истина);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьОшибкуОрфографии(ТаблицаОшибокОрфографии, Текст, НомерСтроки, МестоПроверки, Дополнительно)
	
	ОшибкаОрфографии = ТаблицаОшибокОрфографии.Добавить();
	ОшибкаОрфографии.Текст = Текст;
	ОшибкаОрфографии.НомерСтроки = НомерСтроки;
	ОшибкаОрфографии.МестоПроверки = МестоПроверки;
	ОшибкаОрфографии.Дополнительно = Дополнительно;
	
КонецПроцедуры

// Процедура выполняет проверку правописания в переданном тексте.
//
Процедура ПроверитьПравописание(Знач ТекстДляПроверки, ТаблицаОшибокОрфографии, ВерныеСлова, СоответствиеСловПроверки,
	МестоПроверки = Неопределено, Дополнительно = Неопределено, РазбиватьСвязныеСлова = Ложь) Экспорт
	
	Если ВерныеСлова = Неопределено Тогда
		ИнициализироватьСловарьВерныхСлов(ВерныеСлова);
	КонецЕсли;
	
	ТекстДляПроверки = СтрЗаменить(ТекстДляПроверки, "Ё", "Е");
	ТекстДляПроверки = СтрЗаменить(ТекстДляПроверки, "ё", "е");
	
	НомерСтроки = 0;
	МассивСтрок = СтрРазделить(ТекстДляПроверки, Символы.ПС, Истина);
	Для Каждого СтрокаМодуля Из МассивСтрок Цикл
		
		НомерСтроки = НомерСтроки + 1;
		
		МассивСловСтроки = СтрРазделить(СтрокаМодуля, " ", Ложь);
		
		Для Каждого СвязноеСлово Из МассивСловСтроки Цикл
			
			СловоНаписаноВерно = СоответствиеСловПроверки[СвязноеСлово];
			Если СловоНаписаноВерно = Истина Тогда
				Продолжить;
			ИначеЕсли СловоНаписаноВерно = Ложь Тогда
				ЗаписатьОшибкуОрфографии(ТаблицаОшибокОрфографии, СвязноеСлово, НомерСтроки, МестоПроверки, Дополнительно);
				Продолжить;
			КонецЕсли;
			
			Если ВерныеСлова[ВРег(СвязноеСлово)] = Истина Тогда
				СоответствиеСловПроверки.Вставить(СвязноеСлово, Истина);
				Продолжить;
			КонецЕсли;
			
			Если РазбиватьСвязныеСлова Тогда
				МассивСлов = ПолучитьМассивСловПоЗаглавнымБуквам(СвязноеСлово);
			Иначе
				МассивСлов = Новый Массив;
				МассивСлов.Добавить(СвязноеСлово);
			КонецЕсли;
			
			СвязноеСловоНаписаноВерно = Истина;
			Для Каждого Слово Из МассивСлов Цикл
				
				ОтдельноеСловоНаписаноВерно = СоответствиеСловПроверки[Слово];
				Если ОтдельноеСловоНаписаноВерно = Истина Тогда
					Продолжить;
				ИначеЕсли ОтдельноеСловоНаписаноВерно = Ложь Тогда
					ЗаписатьОшибкуОрфографии(ТаблицаОшибокОрфографии, Слово, НомерСтроки, МестоПроверки, Дополнительно);
					СвязноеСловоНаписаноВерно = Ложь;
					Продолжить;
				КонецЕсли;
				
				Если (ВерныеСлова[ВРег(Слово)] = Истина)
				 ИЛИ (СтрДлина(Слово) = 1)
				 ИЛИ ЭтоЧисло(Слово)
				 ИЛИ СловоСодержитЛатиницу(Слово) Тогда
					
					СоответствиеСловПроверки.Вставить(Слово, Истина);
					Продолжить;
				КонецЕсли;
				
				ЗаписатьОшибкуОрфографии(ТаблицаОшибокОрфографии, Слово, НомерСтроки, МестоПроверки, Дополнительно);
				СоответствиеСловПроверки.Вставить(Слово, Ложь);
				СвязноеСловоНаписаноВерно = Ложь;
			КонецЦикла;
			
			Если СвязноеСловоНаписаноВерно Тогда
				СоответствиеСловПроверки.Вставить(СвязноеСлово, Истина);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Функция возвращает строку, полученную путем преобразования переданной строки кода или текстового документа с кодом.
// Выполняется преобразование:
//  1. Убираются комментарии.
//  2. Символы форматирования заменяются на пробелы.
//  3. Сокращаются повторяющиеся пробелы.
//  4. Сокращаются повторяющиеся ";".
//  5. Образуются пробелы слева и справа.
//  6. Выполняется перевод в верхний регистр.
//  7. Суммируется через пробел в результирующую строку.
//
Функция ПолучитьДайджестСтрокиКода(СтрокаКода) Экспорт
	
	Буфер = Новый ТекстовыйДокумент;
	
	Если ТипЗнч(СтрокаКода) = Тип("Строка") Тогда
		Буфер.УстановитьТекст(СтрокаКода);
	ИначеЕсли ТипЗнч(СтрокаКода) = Тип("ТекстовыйДокумент") Тогда
		Буфер.УстановитьТекст(СтрокаКода.ПолучитьТекст());
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = "";
	
	ВсегоСтрок = Буфер.КоличествоСтрок();
	
	Для Счетчик = 1 По ВсегоСтрок Цикл
		Порция = Буфер.ПолучитьСтроку(Счетчик);
		
		// 1. Убираются комментарии.
		ПозицияКомментария = СтрНайти(Порция, "//");
		Если ПозицияКомментария > 0 Тогда
			Порция = Лев(Порция, ПозицияКомментария - 1);
		КонецЕсли;
		
		// 2. Символы форматирования заменяются на пробелы.
		Порция = СтрЗаменить(Порция, Символы.ВК, 	" ");
		Порция = СтрЗаменить(Порция, Символы.ВТаб, 	" ");
		Порция = СтрЗаменить(Порция, Символы.НПП, 	" ");
		Порция = СтрЗаменить(Порция, Символы.ПС, 	" ");
		Порция = СтрЗаменить(Порция, Символы.ПФ, 	" ");
		Порция = СтрЗаменить(Порция, Символы.Таб, 	" ");
		
		// 3. Сокращаются повторяющиеся пробелы.
		Пока СтрНайти(Порция, "  ") > 0 Цикл
			Порция = СтрЗаменить(Порция, "  ", " ");
		КонецЦикла;
		
		// 4. Сокращаются повторяющиеся ";".
		Пока СтрНайти(Порция, ";;") > 0 Цикл
			Порция = СтрЗаменить(Порция, ";;", ";");
		КонецЦикла;
		
		// 5. Обрезаются пробелы слева и справа.
		Порция = СокрЛП(Порция);
		
		// 6. Выполняется перевод в верхний регистр.
		Порция = ВРЕГ(Порция);
		
		// 7. Суммируется через пробел в результирующую строку.
		Если НЕ ПустаяСтрока(Порция) Тогда
			Результат = Результат + Порция;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиПодписокНаСобытия

// Процедура реализует обработчик событий обработки удаления проведения документа.
// Предназначения для пометки недействительными ошибок, которые записываются в результате проведения этого документа.
//
Процедура ОбработкаУдаленияПроведенияПроверкаВерсии(Источник, Отказ) Экспорт
	
	// Отмечаем ошибки, которые выпадают из поля проверки.
	ЗапросПоПравиламДокумента = Новый Запрос;
	ЗапросПоПравиламДокумента.Текст = "
	|ВЫБРАТЬ
	|	ТребованияРеализацияТребования.ПравилоПроверки КАК Правило
	|ИЗ
	|	Справочник.Требования.РеализацияТребования КАК ТребованияРеализацияТребования
	|ГДЕ
	|	ТребованияРеализацияТребования.Ссылка В
	|		(ВЫБРАТЬ
	|			ПроверкаВерсииСоставТребований.Требование КАК Требование
	|		ИЗ
	|			Документ.ПроверкаВерсии.СоставТребований КАК ПроверкаВерсииСоставТребований
	|		ГДЕ
	|			ПроверкаВерсииСоставТребований.Ссылка = &Ссылка)
	|	И НЕ ТребованияРеализацияТребования.ПравилоПроверки.РучнаяПроверка";
	
	ЗапросПоПравиламДокумента.УстановитьПараметр("Ссылка", Источник.Ссылка);
	ПравилаДокумента = ЗапросПоПравиламДокумента.Выполнить().Выгрузить().ВыгрузитьКолонку("Правило");
	
	ЗапросПоПравиламДругихДокументов = Новый Запрос;
	ЗапросПоПравиламДругихДокументов.Текст = "
	|ВЫБРАТЬ
	|	ТребованияРеализацияТребования.ПравилоПроверки КАК Правило
	|ИЗ
	|	Справочник.Требования.РеализацияТребования КАК ТребованияРеализацияТребования
	|ГДЕ
	|	ТребованияРеализацияТребования.Ссылка В
	|		(ВЫБРАТЬ
	|			ПроверкаВерсииСоставТребований.Требование КАК Требование
	|		ИЗ
	|			Документ.ПроверкаВерсии.СоставТребований КАК ПроверкаВерсииСоставТребований
	|		ГДЕ
	|			  (ПроверкаВерсииСоставТребований.Ссылка <> &Ссылка)
	|			И (ПроверкаВерсииСоставТребований.Ссылка.Версия = &Версия)
	|			И (НЕ ПроверкаВерсииСоставТребований.Ссылка.ПометкаУдаления))
	|	И НЕ ТребованияРеализацияТребования.ПравилоПроверки.РучнаяПроверка";
	
	ЗапросПоПравиламДругихДокументов.УстановитьПараметр("Ссылка", Источник.Ссылка);
	ЗапросПоПравиламДругихДокументов.УстановитьПараметр("Версия", Источник.Версия);
	
	ПравилаДругихДокументов = ЗапросПоПравиламДругихДокументов.Выполнить().Выгрузить().ВыгрузитьКолонку("Правило");
	
	Для Каждого ОбщееПравило Из ПравилаДругихДокументов Цикл
		ИндексПравила = ПравилаДокумента.Найти(ОбщееПравило);
		Если ИндексПравила <> Неопределено Тогда
			ПравилаДокумента.Удалить(ИндексПравила);
		КонецЕсли;
	КонецЦикла;
	
	// Получаем список ошибок для удаления.
	ЗапросПоОшибкам = Новый Запрос;
	ЗапросПоОшибкам.Текст = "
	|ВЫБРАТЬ
	|	НайденныеОшибки.Номер,
	|	НайденныеОшибки.Правило,
	|	НайденныеОшибки.Объект
	|ИЗ
	|	РегистрСведений.НайденныеОшибки КАК НайденныеОшибки
	|ГДЕ
	|	НайденныеОшибки.Объект.Владелец = &Версия
	|	И НайденныеОшибки.Правило В(&Правила)
	|	И НайденныеОшибки.Состояние <> &Состояние";
	
	ЗапросПоОшибкам.УстановитьПараметр("Версия", Источник.Версия);
	ЗапросПоОшибкам.УстановитьПараметр("Правила", ПравилаДокумента);
	ЗапросПоОшибкам.УстановитьПараметр("Состояние", Перечисления.СостояниеОшибки.Особенность);
	
	ВыборкаОшибок = ЗапросПоОшибкам.Выполнить().Выбрать();
	
	МенеджерОшибки = РегистрыСведений.НайденныеОшибки.СоздатьМенеджерЗаписи();
	
	Пока ВыборкаОшибок.Следующий() Цикл
		// Удаляем ошибку, так как она не действительна.
		МенеджерОшибки.Номер = ВыборкаОшибок.Номер;
		МенеджерОшибки.Объект = ВыборкаОшибок.Объект;
		МенеджерОшибки.Правило = ВыборкаОшибок.Правило;
		
		МенеджерОшибки.Удалить();
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура реализует обработчик событий перед записью документа.
// Предназначения для проверки возможности редактирования документа.
// Запрещает редактирование документа ранее даты запрета редактирования.
//
Процедура ПередЗаписьюДокумента(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросПоДате = Новый Запрос;
	ЗапросПоДате.Текст = "
	|ВЫБРАТЬ
	|	Константы.ДатаЗапретаРедактирования КАК ДатаЗапретаРедактирования
	|ИЗ
	|	Константы КАК Константы";
	
	Выборка = ЗапросПоДате.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если Источник.Дата <= Выборка.ДатаЗапретаРедактирования Тогда
		Отказ = Истина;
		#Если Клиент Тогда
		Предупреждение(НСтр("ru='Нельзя изменять документы с датой, ранее даты запрета редактирования.'"));
		#КонецЕсли
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Разбиваем слово на несколько по заглавным буквам.
//
Функция ПолучитьМассивСловПоЗаглавнымБуквам(Знач Слово) Экспорт
	
	Если ПустаяСтрока(Слово) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Строка = "";
	
	ДлинаСлова = СтрДлина(Слово);
	Для Счетчик = 1 По ДлинаСлова Цикл
		
		Буква = Сред(Слово, Счетчик, 1);
		Если Буква = ВРег(Буква) Тогда
			Строка = Строка + " ";
		КонецЕсли;
		
		Строка = Строка + Буква;
		
	КонецЦикла;
	
	Возврат СтрРазделить(Строка, " ", Ложь);
	
КонецФункции

#КонецОбласти
