
#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПараметрыПисьма() Экспорт
	
	ПараметрыПисьма = Новый Структура;
	
	ПараметрыПисьма.Вставить("Тема", "");
	ПараметрыПисьма.Вставить("Тело", "");
	ПараметрыПисьма.Вставить("АдресПолучателя", "");
	ПараметрыПисьма.Вставить("ИмяПолучателя");
	ПараметрыПисьма.Вставить("АдресОтправителя");
	ПараметрыПисьма.Вставить("ИмяОтправителя");
	ПараметрыПисьма.Вставить("ПарольОтправителя");
	ПараметрыПисьма.Вставить("СерверИсходящейПочтыSMTP");
	ПараметрыПисьма.Вставить("ПользовательИсходящейПочты");
	ПараметрыПисьма.Вставить("ПортSMTP");
	
	Возврат ПараметрыПисьма;
	
КонецФункции

// Возвращает структуру с параметрами письма
//
Функция ПодготовитьПараметрыПисьма(ПараметрыПисьма)
	
	Если ПараметрыПисьма.ИмяПолучателя = Неопределено Тогда
		ПараметрыПисьма.ИмяПолучателя = ПараметрыПисьма.АдресПолучателя;
	КонецЕсли;
	
	Если ПараметрыПисьма.АдресОтправителя = Неопределено Тогда
		ПараметрыПисьма.АдресОтправителя = Константы.АдресЭлектроннойПочты.Получить();
	КонецЕсли;
	
	Если ПараметрыПисьма.ИмяОтправителя = Неопределено Тогда
		ПараметрыПисьма.ИмяОтправителя = Константы.ИмяОтправителя.Получить();
	КонецЕсли;
	
	Если ПараметрыПисьма.ПарольОтправителя = Неопределено Тогда
		ПараметрыПисьма.ПарольОтправителя = Константы.ПарольЭлектроннойПочты.Получить();
	КонецЕсли;
	
	Если ПараметрыПисьма.СерверИсходящейПочтыSMTP = Неопределено Тогда
		ПараметрыПисьма.СерверИсходящейПочтыSMTP = Константы.СерверИсходящейПочтыSMTP.Получить();
	КонецЕсли;
	
	Если ПараметрыПисьма.ПользовательИсходящейПочты = Неопределено Тогда
		ПараметрыПисьма.ПользовательИсходящейПочты = Константы.ПользовательИсходящейПочты.Получить();
	КонецЕсли;
	
	Если ПараметрыПисьма.ПортSMTP = Неопределено Тогда
		ПараметрыПисьма.ПортSMTP = Константы.ПортSMTP.Получить();
	КонецЕсли;
	
	Возврат ПараметрыПисьма;
	
КонецФункции

// Отправляет письма о проверке конфигурации ответственным за ошибки.
//
Процедура ОтправитьПисьмаОтветственным(ДокументПроверки, Текст, ФлагОшибка = Ложь) Экспорт
	
	Если НЕ ЗаполненыОсновныеПараметрыСистемнойЭлектроннойПочты() Тогда
		Возврат;
	КонецЕсли;
	
	Конфигурация = ДокументПроверки.Конфигурация;
	
	Если ФлагОшибка Тогда
		ОбщийТекст = НСтр("ru='Возникли ошибки при проверке конфигурации %1 средствами АПК.'");
	Иначе
		ОбщийТекст = НСтр("ru='Выполнена проверка %1 средствами АПК.'");
	КонецЕсли;
	
	ОбщийТекст = СтрШаблон(ОбщийТекст, Конфигурация);
	ТекстТемаПисьма = ОбщийТекст + ?(ФлагОшибка, "", " " + Текст);
	
	ШаблонПисьма = "";
	Если ФлагОшибка Тогда
		ТекстПисьма = НСтр("ru='Журнал проверки:'") + Символы.ПС + Текст;
	Иначе
		ТекстПисьма = НСтр("ru='Сводка по ошибкам:'") + Символы.ПС;
		
		ШаблонПисьма = СокрЛП(Конфигурация.ШаблонПисьма);
		Если НЕ ПустаяСтрока(ШаблонПисьма) Тогда
			ШаблонПисьма = ШаблонПисьма + Символы.ПС + Символы.ПС;
		КонецЕсли;
	КонецЕсли;
	
	ТекстПисьмаОтветственному = ШаблонПисьма + ОбщийТекст + Символы.ПС + ТекстПисьма;
	
	ОтветственныйЗаКонфигурацию = Конфигурация.ОтветственныйЗаКонфигурацию;
	
	// Отправка письма ответственным за ошибки.
	ТабДокументОтчет = Новый ТабличныйДокумент;
	ТаблицаЗначенийОтчет = Новый ТаблицаЗначений;
	ТаблицаЗначенийОтчет.Колонки.Добавить("Ответственный");
	ТекстHTML = Неопределено;
	
	Если НЕ ФлагОшибка Тогда
		СформироватьОтчетОшибок(Конфигурация, ТабДокументОтчет, ТаблицаЗначенийОтчет);
		
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".HTML");
		ТабДокументОтчет.Записать(ИмяВременногоФайла, ТипФайлаТабличногоДокумента.HTML3);
		
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.Прочитать(ИмяВременногоФайла);
		ТекстHTML = ТекстовыйДокумент.ПолучитьТекст();
		ФайлУдалить(ИмяВременногоФайла);
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ОтветственныйЗаКонфигурацию.АдресЭлектроннойПочты)
		И ОтветственныйЗаКонфигурацию.ФлагРассылки Тогда
		
		СтрокаТЗ = ТаблицаЗначенийОтчет.Добавить();
		СтрокаТЗ.Ответственный = ОтветственныйЗаКонфигурацию;
	КонецЕсли;
	
	ТаблицаЗначенийОтчет.Свернуть("Ответственный");
	
	РезультатОтправкиПисем = "";
	ЕстьОтветственныеДляОтправкиПисем = Ложь;
	Для Каждого СтрокаТаблицы Из ТаблицаЗначенийОтчет Цикл
		
		ОтветственныйЗаОшибки = СтрокаТаблицы.Ответственный;
		Если НЕ ЗначениеЗаполнено(ОтветственныйЗаОшибки) Тогда
			Продолжить;
		КонецЕсли;
		
		АдресЭлектроннойПочты = ОтветственныйЗаОшибки.АдресЭлектроннойПочты;
		
		Если ПустаяСтрока(АдресЭлектроннойПочты) ИЛИ (НЕ ОтветственныйЗаОшибки.ФлагРассылки) Тогда
			Продолжить;
		КонецЕсли;
		
		ЕстьОтветственныеДляОтправкиПисем = Истина;
		
		ПараметрыПисьма = ПолучитьПараметрыПисьма();
		ПараметрыПисьма.Тело = ТекстПисьмаОтветственному;
		ПараметрыПисьма.Тема = ТекстТемаПисьма;
		ПараметрыПисьма.АдресПолучателя = АдресЭлектроннойПочты;
		ПараметрыПисьма.ИмяПолучателя = ОтветственныйЗаОшибки.Наименование;
		
		ПараметрыПисьма = ПодготовитьПараметрыПисьма(ПараметрыПисьма);
		
		ОписаниеОшибки = "";
		УспешноеВыполнение = ОтправитьСообщение(ПараметрыПисьма, ОписаниеОшибки, ТекстHTML);
		Если УспешноеВыполнение Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru='Отправлено письмо ответственному по адресу %1'"), АдресЭлектроннойПочты);
		Иначе
			ТекстСообщения = СтрШаблон(НСтр("ru='Не удалось отправить письмо ответственному по адресу %1.
				|%2'"), АдресЭлектроннойПочты, ОписаниеОшибки);
		КонецЕсли;
		
		РезультатОтправкиПисем = СтрШаблон("%1%2%3", РезультатОтправкиПисем, Символы.ПС, ТекстСообщения);
		
	КонецЦикла;
	
	Если НЕ ЕстьОтветственныеДляОтправкиПисем Тогда
		Возврат;
	КонецЕсли;
	
	// Запишем результат отправки писем в журнал документа проверки версии.
	ЖурналПроверки = ДокументПроверки.ЖурналПроверки;
	СообщениеОбОтправке = НСтр("ru='Выполняется отправка писем ответственным:'") + РезультатОтправкиПисем;
	Зафиксировать(Конфигурация, УровеньЖурналаРегистрации.Информация, СообщениеОбОтправке, ЖурналПроверки);
	
	ДокументПроверкиОбъект = ДокументПроверки.ПолучитьОбъект();
	ДокументПроверкиОбъект.ЖурналПроверки = ЖурналПроверки;
	ДокументПроверкиОбъект.Записать(РежимЗаписиДокумента.Запись);
	
КонецПроцедуры

// Отправляет письмо по параметрам письма
//
Функция ОтправитьСообщение(ПараметрыПисьма, ОписаниеОшибки = "", ТекстHTML = Неопределено, РазделительАдресов = ";") Экспорт
	
	Письмо = Новый ИнтернетПочтовоеСообщение;
	Письмо.Тема = ПараметрыПисьма.Тема;
	
	ИмяПолучателя = ПараметрыПисьма.ИмяПолучателя;
	
	// Заполняем адреса получателей письма.
	МассивАдресов = СтрРазделить(ПараметрыПисьма.АдресПолучателя, РазделительАдресов, Ложь);
	Для Каждого АдресПолучателя Из МассивАдресов Цикл
		АдресПолучателя = СокрЛП(АдресПолучателя);
		Получатель = Письмо.Получатели.Добавить(АдресПолучателя);
		Получатель.ОтображаемоеИмя = ?(ЗначениеЗаполнено(ИмяПолучателя), ИмяПолучателя, АдресПолучателя);
	КонецЦикла;
	
	// Добавляем к письму имя отправителя.
	Письмо.ИмяОтправителя              = ПараметрыПисьма.ИмяОтправителя;
	Письмо.Отправитель.ОтображаемоеИмя = ПараметрыПисьма.ИмяОтправителя;
	Письмо.Отправитель.Адрес           = ПараметрыПисьма.АдресОтправителя;
	
	// Если текст HTML не определен, тогда добавляем обычный текст.
	Если ТекстHTML = Неопределено Тогда
		Текст = Письмо.Тексты.Добавить(ПараметрыПисьма.Тело, ТипТекстаПочтовогоСообщения.ПростойТекст);
	Иначе
		// Иначе добавим текст из тела письма в HTML.
		НовыйТекстHTML = РазборФайловHTML.ДобавитьОбычныйТекстВТекстHTML(ТекстHTML, ПараметрыПисьма.Тело);
		Текст = Письмо.Тексты.Добавить(НовыйТекстHTML, ТипТекстаПочтовогоСообщения.HTML);
		Письмо.ОбработатьТексты();
	КонецЕсли;
	
	Профиль = Новый ИнтернетПочтовыйПрофиль;
	Профиль.Пользователь       = ПараметрыПисьма.ПользовательИсходящейПочты;
	Профиль.Пароль             = ПараметрыПисьма.ПарольОтправителя;
	Профиль.ВремяОжидания      = 30;
	Профиль.АдресСервераSMTP   = ПараметрыПисьма.СерверИсходящейПочтыSMTP;
	Профиль.ПортSMTP           = ПараметрыПисьма.ПортSMTP;
	Профиль.АутентификацияSMTP = СпособSMTPАутентификации.ПоУмолчанию;
	Профиль.ПользовательSMTP   = ПараметрыПисьма.ПользовательИсходящейПочты;
	Профиль.ПарольSMTP         = Профиль.Пароль;
	
	Попытка
		Соединение = Новый ИнтернетПочта;
		Соединение.Подключиться(Профиль);
		Соединение.Послать(Письмо);
		Соединение.Отключиться();
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

// Заполняет настройки константы "ПортSMTP" по умолчанию
//
Функция ЗаполнениеНастроекПоУмолчанию() Экспорт
	
	ПортSMTP = Константы.ПортSMTP.Получить();
	Если ПортSMTP = 0 Тогда
		Константы.ПортSMTP.Установить(25);
	КонецЕсли;
	
КонецФункции

// Отправляет письма ответственным о назначении им новых ошибок.
//
Процедура ОтправитьПисьмаОтветственнымОПереназначенныхОшибках() Экспорт
	
	Если НЕ ЗаполненыОсновныеПараметрыСистемнойЭлектроннойПочты() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	УведомленияОтветственных.Получатель КАК Получатель,
	|	УведомленияОтветственных.Получатель.Наименование КАК ИмяПолучателя,
	|	УведомленияОтветственных.Получатель.АдресЭлектроннойПочты КАК АдресЭлектроннойПочты,
	|	УведомленияОтветственных.Получатель.ФлагРассылки КАК ФлагРассылки,
	|	УведомленияОтветственных.Конфигурация КАК Конфигурация,
	|	УведомленияОтветственных.Конфигурация.Наименование КАК ИмяКонфигурации,
	|	УведомленияОтветственных.Отправитель КАК Отправитель,
	|	УведомленияОтветственных.Отправитель.Наименование КАК ИмяОтправителя,
	|	УведомленияОтветственных.ОбъектыМетаданных КАК ОбъектыМетаданных,
	|	УведомленияОтветственных.КоличествоОшибок КАК КоличествоОшибок
	|ИЗ
	|	РегистрСведений.УведомленияОтветственных КАК УведомленияОтветственных
	|ИТОГИ ПО
	|	Получатель";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	МассивПолучателей = Новый Массив;
	
	ВыборкаПолучатель = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПолучатель.Следующий() Цикл
		
		АдресЭлектроннойПочты = ВыборкаПолучатель.АдресЭлектроннойПочты;
		Если ПустаяСтрока(АдресЭлектроннойПочты) ИЛИ (НЕ ВыборкаПолучатель.ФлагРассылки) Тогда
			Продолжить;
		КонецЕсли;
		
		ТемаПисьма = НСтр("ru='Вам направлены ошибки АПК (%1)'");
		ТекстПисьма = "";
		
		ОбщееКоличествоОшибок = 0;
		
		ВыборкаДетальныеЗаписи = ВыборкаПолучатель.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			КоличествоОшибок = ВыборкаДетальныеЗаписи.КоличествоОшибок;
			ОбщееКоличествоОшибок = ОбщееКоличествоОшибок + КоличествоОшибок;
			
			// Получим правильное склонение слова "ошибки" (направил 1 ошибку, 2 ошибки, 5 ошибок).
			ТекстОшибки = ЧислоПрописью(КоличествоОшибок, НСтр("ru='Л=ru_RU'"), НСтр("ru='ошибку, ошибки, ошибок, ж,,,,, 0'"));
			МассивСлов = СтрРазделить(ТекстОшибки, " ", Ложь);
			СловоОшибки = МассивСлов[МассивСлов.ВГраница()];
			
			ТекстПисьма = СтрШаблон(НСтр("ru='%1Пользователь %2 направил Вам %3 %4 в конфигурации ""%5"" по следующим объектам метаданных:%6'"),
				ТекстПисьма,
				ВыборкаДетальныеЗаписи.ИмяОтправителя,
				КоличествоОшибок,
				СловоОшибки,
				ВыборкаДетальныеЗаписи.ИмяКонфигурации,
				Символы.ПС);
			
			МассивПутейОбъектов = Новый Массив;
			
			ОбъектыМетаданных = ВыборкаДетальныеЗаписи.ОбъектыМетаданных.Получить();
			Если ЗначениеЗаполнено(ОбъектыМетаданных) Тогда
				Для Каждого ОбъектМетаданных Из ОбъектыМетаданных Цикл
					МассивПутейОбъектов.Добавить(ОбъектМетаданных.Путь);
				КонецЦикла;
			КонецЕсли;
			
			ТекстОбъектовМетаданных = СтрСоединить(МассивПутейОбъектов, Символы.ПС);
			
			ТекстПисьма = ТекстПисьма + ТекстОбъектовМетаданных + Символы.ПС + Символы.ПС;
			
		КонецЦикла;
		
		ТемаПисьма = СтрШаблон(ТемаПисьма, ОбщееКоличествоОшибок);
		
		ПараметрыПисьма = ПолучитьПараметрыПисьма();
		ПараметрыПисьма.Тело = ТекстПисьма;
		ПараметрыПисьма.Тема = ТемаПисьма;
		ПараметрыПисьма.АдресПолучателя = АдресЭлектроннойПочты;
		ПараметрыПисьма.ИмяПолучателя = ВыборкаПолучатель.ИмяПолучателя;
		
		ПараметрыПисьма = ПодготовитьПараметрыПисьма(ПараметрыПисьма);
		
		УспешноеВыполнение = ОтправитьСообщение(ПараметрыПисьма, "");
		Если УспешноеВыполнение Тогда
			МассивПолучателей.Добавить(ВыборкаПолучатель.Получатель);
		КонецЕсли;
		
	КонецЦикла;
	
	Если МассивПолучателей.Количество() > 0 Тогда
		УдалитьЗаписиВРегистреУведомленияОтветственных(МассивПолучателей);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
