#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ЭтоКопия Экспорт;    // Флаг копирования объекта.
Перем ОбъектКопия Экспорт; // Содержит ссылку на источник копирования.

#КонецОбласти

#Область ОбработчикиСобытий

// Обработчик ПередЗаписью
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ЭтоКопия = Истина;
	ОбъектКопия = ОбъектКопирования.Ссылка;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоКопия Тогда
		СкопироватьКонфигурацию(ОбъектКопия, Ссылка);
		ЭтоКопия = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СкопироватьКонфигурацию(Источник, Приемник)
	
	ВерсияИсточник = НайтиПоследнююВерсию(Источник, Ложь);
	Если ЗначениеЗаполнено(ВерсияИсточник) Тогда
		ВерсияПриемник = СкопироватьВерсиюКонфигурации(Источник, Приемник, ВерсияИсточник);
		СкопироватьСтруктуруКонфигурации(ВерсияИсточник, ВерсияПриемник);
		ТаблицаНомеровОшибок = СкопироватьОшибкиКонфигурации(ВерсияИсточник, ВерсияПриемник);
		СкопироватьКомментарииНайденныхОшибок(ТаблицаНомеровОшибок);
	Иначе
		Сообщить(НСтр("ru='Не найдено ни одной версии конфигурации-источника.'") + " "
			+ НСтр("ru='Версия и структура конфигурации не будут скопированы.'"));
	КонецЕсли;
	
КонецПроцедуры

Функция СкопироватьОшибкиКонфигурации(ВерсияИсточник, ВерсияПриемник)
	
	ТаблицаНомеровОшибок = Новый ТаблицаЗначений;
	ТаблицаНомеровОшибок.Колонки.Добавить("СтарыйНомер", Новый ОписаниеТипов("Число"));
	ТаблицаНомеровОшибок.Колонки.Добавить("НовыйНомер", Новый ОписаниеТипов("Число"));
	ТаблицаНомеровОшибок.Колонки.Добавить("Объект", Новый ОписаниеТипов("СправочникСсылка.СтруктураКонфигурации"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	НайденныеОшибки.Правило,
	|	НайденныеОшибки.Номер КАК Номер,
	|	НайденныеОшибки.Ошибка,
	|	НайденныеОшибки.Состояние,
	|	НайденныеОшибки.Ответственный,
	|	НайденныеОшибки.АвторОсобенности,
	|	НайденныеОшибки.ДатаПомещенияВОсобенности,
	|	НайденныеОшибки.МестоОбнаружения,
	|	НайденныеОшибки.Уточнение,
	|	НайденныеОшибки.ДатаМодификации,
	|	НайденныеОшибки.ПричинаОсобенности,
	|	ЕСТЬNULL(СтруктураКонфигурации.Ссылка, &ПустаяСсылка) КАК Объект
	|ИЗ
	|	РегистрСведений.НайденныеОшибки КАК НайденныеОшибки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтруктураКонфигурации КАК СтруктураКонфигурации
	|		ПО (НайденныеОшибки.Объект.Путь = СтруктураКонфигурации.Путь)
	|		 И (НайденныеОшибки.Объект.ТипОбъекта = СтруктураКонфигурации.ТипОбъекта)
	|		 И (СтруктураКонфигурации.Владелец = &ВерсияПриемник)
	|ГДЕ
	|	НайденныеОшибки.Объект.Владелец = &ВерсияИсточник
	|
	|УПОРЯДОЧИТЬ ПО
	|	Объект, Номер";
	
	Запрос.УстановитьПараметр("ВерсияИсточник", ВерсияИсточник);
	Запрос.УстановитьПараметр("ВерсияПриемник", ВерсияПриемник);
	Запрос.УстановитьПараметр("ПустаяСсылка", Справочники.СтруктураКонфигурации.ПустаяСсылка());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПоследнийНомерОшибки = ПолучитьМаксимальныйНомерОшибки();
	
	НайденныеОшибкиНаборЗаписей = РегистрыСведений.НайденныеОшибки.СоздатьНаборЗаписей();
	
	Пока Выборка.Следующий() Цикл
		
		ПоследнийНомерОшибки = ПоследнийНомерОшибки + 1;
		
		СтрокаТаблицы = ТаблицаНомеровОшибок.Добавить();
		СтрокаТаблицы.СтарыйНомер = Выборка.Номер;
		СтрокаТаблицы.НовыйНомер = ПоследнийНомерОшибки;
		СтрокаТаблицы.Объект = Выборка.Объект;
		
		НайденныеОшибкиНаборЗаписей.Отбор.Объект.Значение = Выборка.Объект;
		НайденныеОшибкиНаборЗаписей.Отбор.Объект.Использование = Истина;
		НайденныеОшибкиНаборЗаписей.Прочитать();
		
		НоваяЗапись = НайденныеОшибкиНаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		НоваяЗапись.Номер = ПоследнийНомерОшибки;
		
		НайденныеОшибкиНаборЗаписей.Записать();
		
	КонецЦикла;
	
	Возврат ТаблицаНомеровОшибок;
	
КонецФункции

Функция СкопироватьКомментарииНайденныхОшибок(ТаблицаНомеровОшибок)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаНомеровОшибок", ТаблицаНомеровОшибок);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаНомеровОшибок.Объект,
	|	ТаблицаНомеровОшибок.СтарыйНомер,
	|	ТаблицаНомеровОшибок.НовыйНомер
	|ПОМЕСТИТЬ ТаблицаНомеров
	|ИЗ
	|	&ТаблицаНомеровОшибок КАК ТаблицаНомеровОшибок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КомментарииНайденныхОшибок.Комментарий,
	|	ТаблицаНомеров.Объект КАК Объект,
	|	ТаблицаНомеров.НовыйНомер КАК Номер
	|ИЗ
	|	ТаблицаНомеров КАК ТаблицаНомеров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КомментарииНайденныхОшибок КАК КомментарииНайденныхОшибок
	|		ПО ТаблицаНомеров.СтарыйНомер = КомментарииНайденныхОшибок.Номер
	|
	|УПОРЯДОЧИТЬ ПО
	|	Объект, Номер";
	
	ТаблицаКомментарииНайденныхОшибок = Запрос.Выполнить().Выгрузить();
	
	КомментарииНайденныхОшибокНаборЗаписей = РегистрыСведений.КомментарииНайденныхОшибок.СоздатьНаборЗаписей();
	
	Для Каждого КомментарийНайденныхОшибок Из ТаблицаКомментарииНайденныхОшибок Цикл
		
		КомментарииНайденныхОшибокНаборЗаписей.Отбор.Объект.Установить(КомментарийНайденныхОшибок.Объект);
		КомментарииНайденныхОшибокНаборЗаписей.Прочитать();
		
		НоваяЗапись = КомментарииНайденныхОшибокНаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, КомментарийНайденныхОшибок);
		
		КомментарииНайденныхОшибокНаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецФункции

Функция СкопироватьВерсиюКонфигурации(Источник, Приемник, ВерсияИсточник)
	
	МассивСвойствДляИсключения = Новый Массив;
	МассивСвойствДляИсключения.Добавить("Владелец");
	МассивСвойствДляИсключения.Добавить("Родитель");
	МассивСвойствДляИсключения.Добавить("СобранныеДанные");
	
	СвойстваДляИсключения = СтрСоединить(МассивСвойствДляИсключения, ", ");
	
	НовыйЭлемент = Справочники.Версии.СоздатьЭлемент();
	НовыйЭлемент.Владелец = Приемник;
	ЗаполнитьЗначенияСвойств(НовыйЭлемент, ВерсияИсточник,, СвойстваДляИсключения);
	НовыйЭлемент.Записать();
	
	Возврат НовыйЭлемент.Ссылка;
	
КонецФункции

Процедура СкопироватьПодсистемыОбъекта(ОбъектИсточник, ОбъектПриемник)
	
	Для Каждого ПодсистемаИсточник Из ОбъектИсточник.Подсистемы Цикл
		
		ПодсистемаПуть = ПодсистемаИсточник.Подсистема.Путь;
		ВерсияПриемник = ОбъектПриемник.Владелец;
		ПодсистемаСсылка = Справочники.СтруктураКонфигурации.НайтиПоРеквизиту("Путь", ПодсистемаПуть,, ВерсияПриемник);
		
		ПодсистемаПриемник = ОбъектПриемник.Подсистемы.Добавить();
		ПодсистемаПриемник.Подсистема = ПодсистемаСсылка;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СкопироватьСтруктуруКонфигурации(ВерсияИсточник, ВерсияПриемник)
	
	ЗапросПоОбъектам = Новый Запрос;
	ЗапросПоОбъектам.Текст = "
	|ВЫБРАТЬ
	|	СтруктураКонфигурации.Ссылка
	|ИЗ
	|	Справочник.СтруктураКонфигурации КАК СтруктураКонфигурации
	|ГДЕ
	|	СтруктураКонфигурации.Владелец = &Владелец
	|	И НЕ СтруктураКонфигурации.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтруктураКонфигурации.НомерПоПорядку";
		
	ЗапросПоОбъектам.УстановитьПараметр("Владелец", ВерсияИсточник);
	
	Выборка = ЗапросПоОбъектам.Выполнить().Выбрать();
	НомерОбъекта = 0;
	ВсегоОбъектов = Выборка.Количество();
	Пока Выборка.Следующий() Цикл
		
		СсылкаИсточник = Выборка.Ссылка;
		ОбъектПриемник = Справочники.СтруктураКонфигурации.СоздатьЭлемент();
		
		ЗаполнитьЗначенияСвойств(ОбъектПриемник, СсылкаИсточник,, "Владелец, Родитель, Код");
		
		РодительПуть = Выборка.Ссылка.Родитель.Путь;
		РодительСсылка = Справочники.СтруктураКонфигурации.НайтиПоРеквизиту("Путь", РодительПуть,, ВерсияПриемник);
		ОбъектПриемник.Родитель = РодительСсылка;
		ОбъектПриемник.Владелец = ВерсияПриемник;
		
		СкопироватьПодсистемыОбъекта(СсылкаИсточник, ОбъектПриемник);
		
		ЭлементЗаписан = Ложь;
		СчетчикТранзакций = 1;
		
		Пока (НЕ ЭлементЗаписан) И (СчетчикТранзакций < 1000) Цикл
			ЭлементЗаписан = Истина;
			Попытка
				ОбъектПриемник.Записать();
			Исключение
				ЭлементЗаписан = Ложь;
			КонецПопытки;
			СчетчикТранзакций = СчетчикТранзакций + 1;
		КонецЦикла;
		
		НомерОбъекта = НомерОбъекта + 1;
		#Если Клиент Тогда
		ТекстСостояния = НСтр("ru='Выполняется запись структуры конфигурации.'") + " "
			+ СтрШаблон(НСтр("ru='Объект конфигурации №%1 из %2: %3.'"), НомерОбъекта, ВсегоОбъектов, ОбъектПриемник.Путь);
		Состояние(ТекстСостояния);
		#КонецЕсли
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

ЭтоКопия = Ложь;

#КонецОбласти

#КонецЕсли
